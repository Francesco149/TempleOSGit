U0 PutKey(I64 ch=0,I64 sc=0)
{//See [Keyboard Devices/System,1] Keyboard Devices.
  CKeyDevEntry *tempp;
  if (ch||sc) {
    tempp=keydev.put_key_root.next;
    if (!Bt(&Fs->display_flags,DISPLAYf_SILENT)) {
      if (kbd.scan_code & SCF_SCROLL && sys_focus_task==Fs)
	while (kbd.scan_code & SCF_SCROLL)
	  Yield; //Wait on SCROLL LOCK Key
      while (tempp!=&keydev.put_key_root) {
	if ((!(sc&SCF_KEY_DESC) || tempp->flags & KDF_HAS_DESCS) &&
	      (*tempp->put_key)(ch,sc))
	  break;
	tempp=tempp->next;
      }
    }
  }
}

U0 PutChars(U64 ch)
{//Output chars. Up to 8 chars in a single U64.
//Don't use this.  [C:/Doc/HolyC.TXT.Z,1] See Print() shortcut.
  while (ch) {
    PutKey(ch&255,0);
    ch>>=8;
  }
}

U0 PutS(U8 *st)
{//Use [C:/Kernel/StrPrint.CPP.Z,906] Print(). See [Keyboard Devices/System,1] Keyboard Devices.
//Don't use this.  [C:/Doc/HolyC.TXT.Z,1] See Print() shortcut.
  I64 ch;
  U8 *ptr;
  Bool cont=TRUE;
  if (!st) return;
  CKeyDevEntry *tempp=keydev.put_key_root.next;
  if (!Bt(&Fs->display_flags,DISPLAYf_SILENT)) {
    if (kbd.scan_code & SCF_SCROLL && sys_focus_task==Fs)
      while (kbd.scan_code & SCF_SCROLL)
	Yield;
    while (cont && tempp!=&keydev.put_key_root) {
      if (tempp->put_s) {
	if ((*tempp->put_s)(st))
	  break;
      } else {
	ptr=st;
	while (ch=*ptr++)
	  if ((*tempp->put_key)(ch,0))
	    cont=FALSE;
      }
      tempp=tempp->next;
    }
  }
}

U0 KeyDescSet(U8 *fmt,...)
{//Call this from key handler to report desc in [C:/Adam/Training.CPP.Z,146] KeyMap().
  U8 *buf=StrPrintJoin(NULL,fmt,argc,argv);
  StrCpy(keydev.desc,buf);
  keydev.handler=Caller;
  Free(buf);
}

CKeyDevEntry *KeyDevAdd(Bool (*fp_put_key)(I64 ch,I64 sc),
	Bool (*fp_puts)(U8 *st),I64 priority,Bool key_descs=FALSE)
{//Places hook in StdOut chain. See [Keyboard Devices/System,1] Keyboard Devices.
  CKeyDevEntry *tempp,*tempp1=keydev.put_key_root.last;
  tempp=ACAlloc(sizeof(CKeyDevEntry));
  tempp->put_key=fp_put_key;
  tempp->put_s=fp_puts;
  tempp->priority=priority;
  if (key_descs)
    tempp->flags|=KDF_HAS_DESCS;
  while (tempp1->priority>priority)
    tempp1=tempp1->last;
  QueIns(tempp,tempp1);
  return tempp;
}

U0 KeyDevRem(CKeyDevEntry *tempp)
{//Remove StdOut hook and free.
  QueRem(tempp);
  Free(tempp);
}

Bool KDRawPutKey(I64 ch,I64)
{
  if (IsRaw) {
    RawPutChar(ch);
    return TRUE;
  } else
    return FALSE;
}

Bool KDRawPutS(U8 *st)
{
  I64 ch;
  if (IsRaw) {
    while (ch=*st++)
      RawPutChar(ch);
    return TRUE;
  } else
    return FALSE;
}

Bool KDInputFilterPutKey(I64 ch,I64 scan_code)
{
  if (Bt(&Fs->task_flags,TASKf_INPUT_FILTER_TASK)) {
    Msg(MSG_KEY_DOWN,ch,scan_code);
    return TRUE;
  } else
    return FALSE;
}

Bool KDInputFilterPutS(U8 *st)
{
  I64 ch;
  if (Bt(&Fs->task_flags,TASKf_INPUT_FILTER_TASK)) {
    while (ch=*st++)
      Msg(MSG_KEY_DOWN,ch,0);
    return TRUE;
  } else
    return FALSE;
}

U0 CtrlAltDel(I64)
{
  LBts(sys_ctrl_alt_flags,SYSf_CTRL_ALT_DEL);
}

U0 CtrlAltC(I64)
{
  LBts(sys_ctrl_alt_flags,SYSf_CTRL_ALT_C);
}

U0 CtrlAltD(I64)
{
  if (!IsDbg) {
    if (Fs==Gs->idle_task)
      BptS(sys_winmgr_task->rip,sys_winmgr_task);
    else
      BptS(*keydev.ctrl_alt_ret_addr);
  }
}

U0 CtrlAltE(I64)
{
  User;
}

U0 CtrlAltF(I64)
{
  SwapI64(&text.font,&text.aux_font);
}

U0 CtrlAltM(I64)
{
  Mute(!IsMute);
}

U0 CtrlAltT(I64)
{
  LBts(sys_ctrl_alt_flags,SYSf_CTRL_ALT_TAB);
}

U0 CtrlAltV(I64)
{
  VGAFlush;
}

U0 CtrlAltX(I64)
{
  LBts(sys_ctrl_alt_flags,SYSf_CTRL_ALT_X);
}

U0 CtrlAltCBSet(U8 ch,U0 (*fp_handler)(I64 sc),
	U8 *no_shift_desc=NULL,U8 *shift_desc=NULL,Bool in_irq=FALSE)
{//Set callback for <CTRL-ALT-letter>.
  ch=ToUpper(ch)-'A';
  if (ch<26) {
    keydev.fp_ctrl_alt_cbs[ch]=fp_handler;

    Free(keydev.ctrl_alt_no_shift_descs[ch]);
    if (no_shift_desc)
      keydev.ctrl_alt_no_shift_descs[ch]=AStrNew(no_shift_desc);
    else
      keydev.ctrl_alt_no_shift_descs[ch]=NULL;

    Free(keydev.ctrl_alt_shift_descs[ch]);
    if (shift_desc)
      keydev.ctrl_alt_shift_descs[ch]=AStrNew(shift_desc);
    else
      keydev.ctrl_alt_shift_descs[ch]=NULL;

    BEqu(&keydev.ctrl_alt_in_irq_flags,ch,in_irq);
  }
}

U0 KeyDevInit()
{
  keydev.fp_ctrl_alt_cbs	=CAlloc(26*sizeof(U8 *));
  keydev.ctrl_alt_no_shift_descs=CAlloc(26*sizeof(U8 *));
  keydev.ctrl_alt_shift_descs	=CAlloc(26*sizeof(U8 *));
  keydev.ctrl_alt_in_irq_flags	=0;
  MemSet(&keydev.put_key_root,0,sizeof(CKeyDevEntry));
  QueInit(&keydev.put_key_root);
  KeyDevAdd(&KDInputFilterPutKey,&KDInputFilterPutS,0x40000000,FALSE);
  KeyDevAdd(&KDRawPutKey,&KDRawPutS,0x60000000,FALSE);
  CtrlAltCBSet('C',&CtrlAltC,"Cmd /Break Execution",,TRUE);
  CtrlAltCBSet('D',&CtrlAltD,"Cmd /Enter Debugger",,TRUE);
  CtrlAltCBSet('E',&CtrlAltE,"Cmd /Spawn New User Task");
  CtrlAltCBSet('F',&CtrlAltF,"Cmd /Toggle Aux Font");
  CtrlAltCBSet('M',&CtrlAltM,"Cmd /Toggle Mute");
  CtrlAltCBSet('T',&CtrlAltT,"Cmd /Switch Focus to Next Task",,TRUE);
  CtrlAltCBSet('V',&CtrlAltV,"Cmd /VGA Flush",,TRUE);
  CtrlAltCBSet('X',&CtrlAltX,"Cmd /Kill Focused Task",,TRUE);
}
