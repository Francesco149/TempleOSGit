//7 has graphics

F64 t0;

U0 DrawIt(TaskStruct *task,GrBitMap *base)
{
  nounusedwarn task;
  I64 i;
  F64 tt=(tJ-t0)*music_tempo%8.0,z,w,x1,y1,z1,x2,y2,z2;
  BoolI8 first=TRUE;
  GrAllocDepthBuf(base);
  base->flags|=BMF_TRANSFORMATION;
  base->x=30*tt+20.0*Sin(tt)+25;
  base->y=10*tt-15;
  base->z=GR_Z_ALL;
  GrRotXEqu(base->r,0.75*tt);
  GrRotYEqu(base->r,0.45*tt);
  GrRotZEqu(base->r,0.50*tt);
  GrScaleMatEqu(base->r,0.5);
  for (i=-50;i<40;i+=2) {
    if (i&2)
      base->color=LTGRAY;
    else
      base->color=DKGRAY;
    z=10*Cos(i/10.0*pi/8.0)*Cos(tt);
    w=Tri(100+i,200);
    x1=i-3;y1=0;z1=z;
    if (-30<=i<=40) {
      GrLine3(base,i,-10*w,z,i-3,0,z);
      GrLine3(base,i,+10*w,z,i-3,0,z);
    }
    if (!first) {
      base->color=DKGRAY;
      GrLine3(base,x1,y1,z1,x2,y2,z2);
    }
    x2=x1;y2=y1;z2=z1;
    first=FALSE;
  }
}

U0 Song()
{
  t0=tJ;
  SettingsPush; //See [C:/LT/Adam/TaskSettings.APZ,3] SettingsPush
  Fs->draw_it=&DrawIt;
  Fs->task_end_cb=&SndTaskEndCB;
  MusicSettingsRst;
  music_tempo= 2.480;
  music_stacatto_factor= 0.902;
  try {
    PutS("$BG+H,LTCYAN$$FG+H,BLACK$");
    while (!ScanKey) {
	Play("W13qE!AA!etF2G3G!qDF!!etFE!D2eG!3G",
		"A\0dam... \0 \0no \0 \0 \0long\0er \0sees \0Your \0face.\r\n\0 \0 \0");
	Play("qE!AA!etF2G3G!qDF!!etFE!D2eG!3G",
		"Oh! \0 \0 \0What \0 \0a \0wretch\0ed \0bar\0ren \0waste!\r\n\0 \0 \0");
	Play("qACFeB!CB2GqG3E!D",
		" \0 \0 \0 \0 \0 \0 \0 \0Ex\0iled.\r\n\0");
	Play("ACFeB!CB2GqG3E!D");
    }
  } catch
    CatchAll;
  SettingsPop;
}

Song;
