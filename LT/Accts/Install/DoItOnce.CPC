U0 MakeRecoveryDsk(U8 drv_let,U8 *acct_name)
{
//Assumes booted from standard CD-ROM
//with 'B' RAM drive.  You pass
//the hard drive partition with
//OldMBR on it.
  LTPrt *p=Drv2Prt('B');
  U8 buf[80],buf2[80],cd_image[80];

  //  InstallBoot(drv_let,'T');
  SPrintF(cd_image,"%C:/LTCD.ISO",drv_let);

  Fmt('B',TRUE,FALSE,PT_LT);
  Drv('B');
  CopyTree("::/LT","/LT");

  SPrintF(buf2,"/LT/Accts/%s",acct_name);
  MkDir(buf2);

  //Check if RAM disk B has space for user's acct directory
  //If not, just copy CfgMgr directory.
  SPrintF(buf,"%C:/LT/Accts/%s/*",drv_let,acct_name);
  if (TreeSize(buf,NULL,p->spc*BLK_SIZE)<0.9*UnusedDrvSpace('B'))
    SPrintF(buf,"%C:/LT/Accts/%s",drv_let,acct_name);
  else {
    SPrintF(buf,"%C:/LT/Accts/%s/CfgMgr",drv_let,acct_name);
    SPrintF(buf2,"/LT/Accts/%s/CfgMgr",acct_name);
  }
  CopyTree(buf,buf2);

  CopyTree("::/Windows","/Windows");
  CopyTree("::/Linux","/Linux");
  MkDir("/Temp");
  SPrintF(buf,"%C:/0000Boot",drv_let);
  CopyTree(buf,"/0000Boot");

  CDFileCreate(cd_image,"B:/*",NULL,CD_BOOT);
  PutS("\r\nPut Blank CD in drive\r\n");
  PressAKey;
  CDImageWrite('T',cd_image);
  Drv(drv_let);
}

U0 InstallWizard()
{
  TaskStruct *task=Fs->next_task;
  I64 unit;
  U8 ch,*base0,*base1,*ms,*dst,*acct_name;
  U8 *st1,*st2,*old_home_str,*new_home_str;
  BoolI8 mbr;
  ATARepStruct *root=NULL,*tempha;
  I64 num_hints;

  while (task->user_num!=2 && task!=Fs)
    task=task->next_task;
  XTalk(task,"Ed(\"::/LT/Doc/Install.TXZ\");\r");

  PutS("$CM,0,10$This wizard works if you have "
  "a partition ready.  For a new install, answer '$FG,GREEN$Yes$FG$' "
  "to '$FG,GREEN$format?$FG$'.  For an upgrade, answer '$FG,GREEN$No$FG$' to '$FG,GREEN$format?$FG$' "
  " and '$FG,GREEN$No$FG$' to '$FG,GREEN$Delete /LT directory?$FG$'.\r\n\r\n"
  "You can partition the drive or compile the kernel "
  "with more options if you do it by hand, not using this wizard.  The LoseThos "
  "disk partitioner doesn't play well with others.\r\n\r\n"
      "Continue ");
  if (YorN) {
    while (TRUE) {
      PutS("Make-up a Log-In Name (account).\r\n");
      acct_name=PmtStr("User Acct Name (%s): ","User");
      if (FileNameChk(acct_name))
	break;
      Free(acct_name);
    }
    task=SpawnUser;
    task->win_top=Fs->win_top;
    task->win_bottom=(Fs->win_top+Fs->win_bottom)>>2-1;
    Fs-> win_top=task->win_bottom+3;
    task->win_right=Fs->win_right;
    task->win_left=Fs->win_left;
    TaskDerivedValsUpdate(task);
    TaskDerivedValsUpdate;
    WinToTop(Fs);
    XTalk(task,"Mount;\r");
    XTalk(task,"C");
    XTalk(task,"2 ");

    num_hints=ATARep(FALSE,&root);
    PutS("\r\nInclude '$FG,PURPLE$0x$FG$' for hexidecimal numbers.\r\n\r\n");
    while (TRUE) {
      base0=PmtStr("Hard Drive I/O Port Base0  : ");
      if (0<A2I(base0)<=0xFFFF)
	break;
      Free(base0);
    }
    if (1<=A2I(base0)<=num_hints) {
      tempha=ATARepFind(&root,A2I(base0));
      Free(base0);
      base0=MSPrintF("0x%X",tempha->base0);
      base1=MSPrintF("0x%X",tempha->base1);
      ms   =MSPrintF("0x%X",tempha->unit);
      unit=A2I(ms);
      Free(ms);
    } else {
      while (TRUE) {
	base1=PmtStr("Hard Drive I/O Port Base1  : ");
	if (0<A2I(base1)<=0xFFFF)
	  break;
	Free(base1);
      }
      while (TRUE) {
	ms =PmtStr("Unit--$FG,PURPLE$0$FG$=Master or $FG,PURPLE$1$FG$=Slave: ","0");
	unit=A2I(ms);
	Free(ms);
	if (0<=unit<=1)
	  break;
      }
    }
    LinkedListDel(root);
    XTalk(task,"%s\r",base0);
    XTalk(task,"%s\r",base1);
    XTalk(task,"%C",'0'+unit);
    XTalkWithWait(task,"\r");
    PrtRep;
    while (TRUE) {
      dst=PmtStr("\r\nDestination Partition Letter: ");
      if ('A'<=ToUpper(*dst)<='Z')
	break;
      Free(dst);
    }

    CrLf;
    st1=MSPrintF("%C:/LT",dft_drv);
    st2=MSPrintF("%C:/LT",*dst);

    PrintF("$FG,RED$Format %C Partition?$FG$\r\n",*dst);
    if (YorN) {
      CrLf;
      while (TRUE) {
	PrintF(
	"$FG,PURPLE$1$FG$) Use FAT32\r\n"
	"$FG,PURPLE$2$FG$) Use LoseThos Native\r\n"
	    "\r\nFile System Type: ");
	ch=GetChar;
	CrLf;
	if ('1'<=ch<='2')
	  break;
      }
      if (ch=='1')
	Fmt(*dst,TRUE,FALSE,PT_FAT32);
      else
	Fmt(*dst,TRUE,FALSE,PT_LT);
    } else {
      PrintF("\r\n$BK,1$WARNING$BK,0$ if you answer yes to the following "
      "you will lose all LoseThos files in your /LT directory tree.\r\n"
	  "Delete %s Directory ",st2);
      if (YorN)
	DelTree(st2);
      CrLf;
    }
    CopyTree(st1,st2);
    st2=MSPrintF("%C:/0000Boot",*dst);
    MkDir(st2);
    Free(st2);
    st2=MSPrintF("%C:/Temp",*dst);
    MkDir(st2);
    Free(st2);

    old_home_str=sys_home_dir;
    new_home_str=MSPrintF("%C:/LT/Accts/%s",*dst,acct_name);
    MkDir(new_home_str);
    sys_home_dir=AStrNew(new_home_str); //Fool the CfgMgr
    XTalkWithWait(task,
    "InstallBoot('%C');\r"
    "B10x20000\r"
    "C"
    "2p"
    "%s\r"
    "%s\r"
    "%C"
    "\r"   //Exit Drives
    "%s\r" //Acct
    "\r"   //Dsk Cache
	"\r",	//Options
	*dst,base0,base1,'0'+unit,acct_name);
    PutS("$FG,RED$Install Master Boot loader?$FG$");
    mbr=YorN;

    Copy("HOME/CfgMgr/" CFG_LT_MAIN "/Current.AUZ",
	"HOME/CfgMgr/" CFG_LT_MAIN "/Safe.AUZ");

    if (mbr) {
      CrLf;
      Auto("%s\r%s\r%C\r",base0,base1,'0'+unit);
      InstallMasterBoot(*dst);
    }

    Free(new_home_str);
    Free(sys_home_dir);
    sys_home_dir=old_home_str;

    PutS("\r\n$FG,RED$Make Recovery Disk?$FG$");
    if (YorN) {
      XTalkWithWait(task,
      "InstallBoot('%C','T',FALSE);\r"
      "B10x20000\rt3 \r"
      "C"
      "2p"
      "%s\r"
      "%s\r"
      "%C"
      "\r" //Exit Drives
      "\r" //Acct
      "\r" //Dsk Cache
	  "\r",//Options
	  *dst,base0,base1,'0'+unit);
      MakeRecoveryDsk(*dst,acct_name);
    }
    Free(acct_name);
    Free(base0);
    Free(base1);
    Free(ms);
    Free(dst);

    PutS("Reboot Now ");
    if (YorN)
      Reboot;

    Fs->win_top=task->win_top;
    Kill(task);
  }
}

InstallWizard;

