#help_index "Cfg Manager"
#help_file  "::/LT/Doc/CfgMgr"

TaskCmdStruct *CfgRecord(TaskCmdStruct *root=NULL)
{
  TaskCmdStruct *tempc;
  U64 p1,p2;
  if (!root) {
    root=CAlloc(sizeof(TaskCmdStruct));
    root->next=root->last=root;
  }
  while (!Bt(&Fs->parent_task->task_flags,TASKf_CFG_RECORD_DONE)) {
    if (ScanKey(&p1,&p2)) {
      if (p1) {
	tempc=CAlloc(sizeof(TaskCmdStruct));
	tempc->msg_code=MSG_KEY_DOWN;
	tempc->p1=p1;
	tempc->p2=p2;
	InsQue(tempc,root->last);
	PutKey(p1,p2);
      }
    }
    Yield;
  }
  return root;
}

TaskCmdStruct *CfgFlush(TaskCmdStruct *root=NULL)
{
  I64 timeout=5;
  TaskCmdStruct *tempc;
  U64 p1,p2;
  if (!root) {
    root=CAlloc(sizeof(TaskCmdStruct));
    root->next=root->last=root;
  }
  while (timeout--) {
    if (ScanMsg(&p1,&p2,1<<MSG_KEY_DOWN)) {
      if (p1) {
	tempc=CAlloc(sizeof(TaskCmdStruct));
	tempc->msg_code=MSG_KEY_DOWN;
	tempc->p1=p1;
	tempc->p2=p2;
	InsQue(tempc,root->last);
      }
      timeout=5;
    } else
      Yield;
  }
  return root;
}


U8 *PopUpPickCfg(U8 *cfg_dir)
{
  U8 *result=0;
  Ltf *l=LtfNew;
  LTDirEntry *tempm,*root=FilesFind("*.AU?");

  LtfPrintF(l,"$FG,PURPLE$Cfg Manager: %s$FG$\r\n\r\n",cfg_dir);
  LtfPrintF(l,"Copy HOME/CfgMgr/%s/Current.AUZ to\r\n"
	      "a new .AUZ file to save it.\r\n\r\n",cfg_dir);
  tempm=root;
  while (tempm) {
    LtfPrintF(l,"$MU-UL,\"%s\",0x%X$\r\n",tempm->name,tempm);
    tempm=tempm->next;
  }
  LtfPutS(l,"\r\n$MU-UL,\"Record Cfg\",-2$\r\n");
  LtfPutS(l,"$MU-UL,\"Don't Use CfgMgr\",-1$\r\n");
  tempm=PopUpMenu(l);
  if (tempm>0)
    result=StrNew(tempm->name);
  else {
    if (tempm==-2)
      PopUpOk("Don't change Focus Window\r\nwhile recording.\r\n");
    result=tempm;
  }
  LtfDel(l);
  LTDirListDel(root);
  return result;
}

U0 CfgMgr(U8 *cfg_dir,TaskCmdStruct *root)
{
  U8 *filename,*st;
  TaskCmdStruct *tempc;

  BoolI8 old_silent=Silent(ON);
  Cd("HOME/CfgMgr",IsDrvWritable);
  if (*cfg_dir)
    Cd(cfg_dir,IsDrvWritable);
  Silent(old_silent);

  CfgFlush(root);
  if (root->next!=root) {
    tempc=root->next;
    while (tempc!=root) {
      Msg(MSG_KEY_DOWN,tempc->p1,tempc->p2);
      tempc=tempc->next;
    }
    filename=-2;
  } else
    filename=PopUpPickCfg(cfg_dir);

  if (filename>0) {
    try {
      AutoFile(filename);
      while (!Bt(&Fs->parent_task->task_flags,TASKf_CFG_RECORD_DONE))
	Yield;
      if (IsDrvWritable) {
	Silent(ON);
	Copy(filename,"Current.AUZ");
	Silent(old_silent);
      }
    } catch {
      ThrowBreak(Fs->parent_task);
      while (!Bt(&Fs->parent_task->task_flags,TASKf_CFG_RECORD_DONE))
	Yield;
      Fs->catch_except=TRUE;
    }
    Free(filename);
  } else {
    try {
      CfgRecord(root);
      if (filename==-2 && IsDrvWritable) {
	st=SysMacro2Str(root);
	TextFileWrite("Current.AUZ",st);
	Free(st);
      }
    } catch {
      ThrowBreak(Fs->parent_task);
      while (!Bt(&Fs->parent_task->task_flags,TASKf_CFG_RECORD_DONE))
	Yield;
      Fs->catch_except=TRUE;
    }
  }
  SysMacroDel(root);
  Free(root);
  while (!LBtr(&Fs->parent_task->task_flags,TASKf_CFG_RECORD_DONE))
    Yield;
}

public U0 CfgStart(U8 *cfg_dir)
{
  TaskCmdStruct *root=CfgFlush;
  LBtr(&Fs->task_flags,TASKf_CFG_RECORD_DONE);
  AutoStr("CfgMgr(\"%s\",0x%X);",cfg_dir,root);
}

public U0 CfgEnd()
{
  LBts(&Fs->task_flags,TASKf_CFG_RECORD_DONE);
  while (Bt(&Fs->task_flags,TASKf_CFG_RECORD_DONE))
    Yield;
}

#help_index ""
