#define SAMPLE_SIZE	100000

U0 TimeIns()
{
  U64 i,start,end;
  I64 *cached_I64=MAlloc(sizeof(I64)),
     shared_blks,*uncached_I64;
  BlkPool *bp;
  HeapCtrl *hc;

  CPURep;

  shared_blks=1;
  bp=AllocUncachedMemBlks(&shared_blks);
  hc=IndependentHeapCtrlInit(bp,shared_blks<<12);
  uncached_I64=MAlloc(sizeof(I64),hc);

  Preempt(OFF);

//Measure cached time
  start=GetTimeStamp;
  for (i=0;i<SAMPLE_SIZE;i++)
    *cached_I64=i;

  end=GetTimeStamp;
  PrintF("  Cached Cycles: %10,d\r\n",end-start);

//Measure uncached time
  start=GetTimeStamp;
  for (i=0;i<SAMPLE_SIZE;i++)
    *uncached_I64=i;
  end=GetTimeStamp;
  PrintF("Uncached Cycles: %10,d\r\n",end-start);

  PutS("$FG,GREEN$The difference is even greater\r\n"
       "because the loop overhead was not subtracted.\r\n\r\n");
  Free(cached_I64);
  Free(uncached_I64);
  Free2MegMemBlks(bp,shared_blks);
  Preempt(ON);
}

TimeIns;

/*  Program Output

8 Cores 2.660GHz
  Cached Cycles:    202,220
Uncached Cycles: 14,357,016
The difference is even greater
because the loop overhead was not subtracted.
*/
