U0 FunDraw1(GrBitMap *base,F64 (*f)(F64 t))
{
  I64 i,w=Fs->win_pixel_width,h=Fs->win_pixel_height;
  F64 scale=h/5.0;
  for (i=-w/2;i<=w/2;i++)
    GrLine(base,w/2+i,h/2-scale*f(i/scale*2*pi),w/2+i+1,h/2-scale*f((i+1)/scale*2*pi));
}

U0 FunDraw2(GrBitMap *base,F64 (*f)(F64 t,F64 p),F64 T)
{
  I64 i,w=Fs->win_pixel_width,h=Fs->win_pixel_height;
  F64 scale=h/5.0;
  for (i=-w/2;i<=w/2;i++)
    GrLine(base,w/2+i,h/2-scale*f(i/scale,T),w/2+i+1,h/2-scale*f((i+1)/scale,T));
}

U0 Init(GrBitMap *base)
{
  I64 i,w=Fs->win_pixel_width,h=Fs->win_pixel_height;
  F64 scale=h/5.0;
  LtfClear;
  GrClear(base);
  base->color=BLACK;
  GrLine(base,0,h/2,w-1,h/2);
  GrLine(base,w/2,0,w/2,h-1);
  for (i=-2;i<=2;i++) {
    base->color=BLACK;
    GrLine(base,w/2-3,h/2-scale*i,w/2+3,h/2-scale*i);
    if (i) {
      base->color=DKGRAY;
      GrPrintF(base,w/2+5,h/2-scale*i-FONT_HEIGHT/2,"%5.1f",ToF64(i));
    }
  }
  for (i=-4;i<=4;i++) {
    base->color=BLACK;
    GrLine(base,w/2+scale*i,h/2-3,w/2+scale*i,h/2+3);
    if (i) {
      base->color=DKGRAY;
      GrPrintF(base,w/2+scale*i-7*FONT_WIDTH/2,h/2+5+FONT_HEIGHT,"%5.1f*T",ToF64(i));
    }
  }
}

U0 SndMathDemo()
{
  F64 y,y1,t,dt;
  GrBitMap *base=GrAlias(gr_persistent_base,Fs);

  Init(base);
  base->color=RED;
  FunDraw1(base,&Sin);
  PutS("$FG,RED$Sin$FG$\r\n");
  PressAKey;
  base->color=BLUE;
  FunDraw2(base,&Saw,1.0);
  PutS("$FG,BLUE$Saw$FG$\r\n");
  PressAKey;

  Init(base);
  base->color=RED;
  FunDraw1(base,&Cos);
  PutS("$FG,RED$Cos$FG$\r\n");
  PressAKey;
  base->color=BLUE;
  FunDraw2(base,&Caw,1.0);
  PutS("$FG,BLUE$Caw$FG$\r\n");
  PressAKey;
  base->color=GREEN;
  FunDraw2(base,&FullCaw,1.0);
  PutS("$FG,GREEN$FullCaw$FG$\r\n");
  PressAKey;

  Init(base);
  base->color=RED;
  FunDraw1(base,&Sin);
  PutS("$FG,RED$Sin$FG$\r\n");
  PressAKey;
  base->color=BLUE;
  FunDraw2(base,&Tri,1.0);
  PutS("$FG,BLUE$Tri$FG$\r\n");
  PressAKey;
  base->color=GREEN;
  FunDraw2(base,&FullTri,1.0);
  PutS("$FG,GREEN$FullTri$FG$\r\n");
  PressAKey;

  GrClear(base);
  GrDel(base);
}

SndMathDemo;

