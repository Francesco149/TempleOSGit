//This loads a list of zero terminated
//strings into the sym table and
//gives a name to it.  Strings like
//this are known as Define entries.

//See [::/LT/Adam/Gr/GrScreen.APZ,ST_RAINBOW_10] ST_RAINBOW_10

U0 ShowDigit(U8 ch)
{
//The %Z code displays a Define subentry
  PrintF("$FG,%Z$%d$FG$",ch-'0',
    "ST_RAINBOW_10",ch-'0');
}

U0 ShowPattern(U8 *st)
{
  U8 ch;
  while (ch=*st++)
    ShowDigit(ch);
  CrLf;
}

U0 Digits()
{
  U64 num,ch,i;
  U8 answer[1024];

  LtfClear;
  //Set default background and foreground
  PutS("$WW,1$$BD,DKGRAY$$FD,WHITE$"
	"\r\nThis is a mem game.  "
	"Try to remember the digits and enter them.  "
	"They are colored based on electrical "
	"engineering color codes.\r\n");
  for (i=0;i<10;i++) {
    PrintF("$FG,%Z$%d: %Z\r\n",
      i,"ST_RAINBOW_10",i,i,"ST_RAINBOW_10");
  }
  PutS("$FG$\r\n"); //set to default background
  if (!PressAKey) goto dg_done;

  while (TRUE) {
restart:
    num=0;
    //Set to zeros so we have terminators
    MemSet(answer,0,sizeof(answer));
    while (TRUE) {
      LtfClear;
      PutS("$WW,1$$BD,DKGRAY$$FD,WHITE$\r\n");
      PrintF("Pattern\r\nLength:%d\r\n"
	     "\r\n",
	num+1);
      answer[num++]=RandU32%10+'0';
      ShowPattern(answer);
      if (!PressAKey) goto dg_done;

      LtfClear;
      PutS("$WW,1$$BD,DKGRAY$$FD,WHITE$\r\n");
      PrintF("Guess\r\nLength:%d\r\n"
	     "\r\n",num);
      for (i=0;i<num;i++) {
	ch=GetChar(NULL,FALSE);
	if (ch==CH_SHIFT_ESC || ch==CH_ESC)
	  return;
	ShowDigit(ch);
	if (ch!=answer[i]) {
	  Snd(0);
	  PrintF("\r\nScore:%d\r\n",num);
	  ShowPattern(answer);
	  Beep;
	  Beep;
	  if (!PressAKey) goto dg_done;
	  goto restart;
	}
      }
    }
  }
dg_done:
  PutS("$CL$\r\n");
}

Digits;
