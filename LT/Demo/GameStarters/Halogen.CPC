
#define NUM_ROAD	512
#define ROAD_WIDTH_BY_2	200
#define CAR_WIDTH_BY_2	100

I64 road_x[NUM_ROAD],road_trend;
F64 speed,distance;
I64 road_ptr=0,car_x;
F64 t_last;
BoolI8 crash;

U0 UpdateWin(TaskStruct *task)
{
  I64 h=task->win_pixel_width,v=task->win_pixel_height;
  GrBitMap *base=GrAlias(gr_refreshed_base,task);
  I64 i,x,y,xx;

  BoolI8 old_preempt=Preempt(OFF);
  xx=h>>1-car_x+road_x[road_ptr&(NUM_ROAD-1)];
  Preempt(old_preempt);

  base->color=LTGRAY;
  for (i=0;i<NUM_ROAD;i++) {
    x=h>>1-car_x+road_x[(i+road_ptr)&(NUM_ROAD-1)];
    y=v-0.5*i;
    if (y<v/2) break;
    GrPlot(base,x+ROAD_WIDTH_BY_2-0.4*i,y);
    GrPlot(base,x-ROAD_WIDTH_BY_2+0.4*i,y);
  }
  base->color=WHITE;
 
  x=h>>1-CAR_WIDTH_BY_2;
  if (x<xx-ROAD_WIDTH_BY_2)
    crash=TRUE;
  GrLine(base,x-10,v,x-40,v-100);
  GrLine(base,x+10,v,x+40,v-100);

  x=h>>1+CAR_WIDTH_BY_2;
  if (x>xx+ROAD_WIDTH_BY_2)
    crash=TRUE;
  GrLine(base,x-10,v,x-40,v-100);
  GrLine(base,x+10,v,x+40,v-100);

  base->color=YELLOW;
  if (crash)
    GrPutS(base,h>>1-FONT_WIDTH*4,(v-FONT_HEIGHT)>>1,"Game Over");

  GrDel(base);
}

U0 UpdateRoad()
{
  BoolI8 old_preempt=Preempt(OFF);
  F64 t0=tP;
  distance+=speed*(t0-t_last);
  t_last=t0;
  while (distance>1.0) {
    road_trend=LimitI64(road_trend+SignI64(RandU16%3-1),-5,5);
    road_x[road_ptr&(NUM_ROAD-1)]=
      road_x[(road_ptr-1)&(NUM_ROAD-1)]+=road_trend/3;
    road_ptr++;
    distance-=1.0;
  }
  Preempt(old_preempt);
}

U0 Init()
{
  I64 i,x=0;
  speed=0;
  distance=0;
  road_trend=0;
  road_ptr=0;
  car_x=0;
  for (i=0;i<NUM_ROAD;i++) {
    road_x[i]=x;
    road_trend=LimitI64(road_trend+SignI64(RandU16%3-1),-5,5);
    x+=road_trend/3;
  }
  t_last=tP;
  crash=FALSE;
}


U0 Halogen()
{
  U64 ch=0,sc,msg_code,p1,p2;

  SettingsPush; //See [C:/LT/Adam/TaskSettings.APZ,3] SettingsPush
  Fs->text_attr=BLACK<<4+WHITE;
  Fs->update_win=&UpdateWin;
  WordStat(OFF);

  MenuPush(
"File {"
"  Abort(,CH_SHIFT_ESC);"
"  Exit(,CH_ESC);"
"}"
"Play {"
"  Restart(,CH_CR);"
"  Accelerate(,,SC_CURSOR_UP);"
"  Deccellerate(,,SC_CURSOR_DOWN);"
"  Left(,,SC_CURSOR_LEFT);"
"  Right(,,SC_CURSOR_RIGHT);"
"}"
);
  WinMax;
  Init;
  try {
    do {
      if (msg_code=ScanMsg(&p1,&p2,1<<MSG_KEY_DOWN|1<<MSG_KEY_UP)) {
	if (msg_code==MSG_KEY_DOWN) {
	  ch=p1;sc=p2;
	  switch (ch) {
	    case CH_CR:
	      Init;
	      break;
	  }
	} else if (msg_code==MSG_KEY_UP) {
	  switch (ch) {
	    case 0:
	      switch (sc.u8[0]) {
		case SC_CURSOR_RIGHT:
		case SC_CURSOR_LEFT:
		case SC_CURSOR_UP:
		case SC_CURSOR_DOWN:
		  ch=0; sc=0;
		  break;
	      }
	      break;
	    default:
	      ch=p1; sc=p2;
	  }
	}
      }
      switch (sc.u8[0]) {
	case SC_CURSOR_RIGHT:
	  car_x++;
	  break;
	case SC_CURSOR_LEFT:
	  car_x--;
	  break;
	case SC_CURSOR_UP:
	  speed+=1;
	  if (speed>200) speed=200;
	  break;
	case SC_CURSOR_DOWN:
	  speed-=1;
	  if (speed<0) speed=0;
	  break;
      }
      if (crash) {
	Snd(0);
	Sleep(10);
      } else {
	if (speed)
	  Snd(speed+10);
	else
	  Snd(0);
	Sleep(10);
	UpdateRoad;
      }
    } while (ch!=CH_ESC && ch!=CH_SHIFT_ESC);
  } catch
    CatchAll;
  MenuPop;
  SettingsPop;
}

Halogen;
