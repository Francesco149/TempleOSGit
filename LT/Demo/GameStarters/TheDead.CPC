#define __BIN_1_TYPE 0x00000002
#define __BIN_1_SIZE 0x0000024A
#define __BIN_1 "\x16\x19\x00\x00\x00\x23\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x04\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x04\x00\x00\x00\x00\x00\x00\x00\x1C\x00\x00\x00\x00\x00\x00\x00\x02\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x11\x00\x00\x00\x03\x00\x00\x80\x02\x00\x00\x00\x00\x00\x00\x80\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x80\x10\x00\x00\x00\x12\x00\x00\x00\x00\x00\x00\x00\x12\x00\x00\x00\x11\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x0B\x00\x00\x00\x00\x00\x00\x00\x01\x00\x00\x00\x00\x00\x00\x00\x01\x00\x00\x00\x08\x00\x00\x80\x02\x00\x00\x00\x00\x00\x00\x00\x04\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x0F\x00\x00\x80\x1F\x00\x00\xC0\x3F\x00\x00\xC0\x03\x00\x00\xC0\x1A\x00\x00\x80\x14\x00\x00\x00\x1C\x00\x00\x60\x1D\x00\x00\x18\x1B\x00\x00\xEC\x06\x00\x00\xC0\x01\x00\x00\xD0\x00\x00\x00\x34\x8F\x07\x00\x26\x00\x00\x00\x36\x00\x00\x00\x30\x40\x00\x00\xB0\x7F\x00\x00\xC0\x3F\x00\x00\x00\x00\x00\x00\x00\x1B\x00\x00\x80\x1B\x00\x00\x00\x17\x00\x00\x00\x03\x00\x00\x80\x1B\x00\x00\x00\x1B\x00\x00\x00\x1B\x00\x00\x80\x1B\x00\x00\x80\x1B\x00\x00\x80\x0B\x00\x00\x80\x03\x00\x00\x00\x01\x00\x00\x00\x04\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\xFF\xF0\xFF\x01\x7F\xE3\xFF\x01\xBF\xC3\xFF\x01\x1F\x81\xFF\x01\x1F\x80\xFF\x01\x1F\xD8\xFF\x01\x1F\xD4\xFF\x01\x3F\xDC\xFF\x01\x07\xCC\xFF\x01\x1B\xD8\xFF\x01\x01\xC4\xFF\x01\x01\x00\xFC\x01\x01\x00\xF0\x01\x14\x8F\x07\x00\x26\x00\x00\x01\x14\x00\x00\x01\x30\x40\xFC\x01\x37\x60\xFD\x01\xC7\x20\xFF\x01\x0F\x80\xFF\x01\x0F\xDA\xFF\x01\x3F\xD8\xFF\x01\x3F\xD1\xFF\x01\x3F\xC2\xFF\x01\xBF\xD0\xFF\x01\x3F\xDA\xFF\x01\x3F\xDA\xFF\x01\x3F\xD3\xFF\x01\xBF\xC2\xFF\x01\x3F\xC3\xFF\x01\x3F\xC1\xFF\x01\x3F\x81\xFF\x01\x3F\x04\xFF\x01\x3F\x08\xFF\x01\x7F\xF8\xFF\x01\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x04\x00\x00\x00\x19\x00\x00\x00\x14\x00\x00\x00\x1C\x00\x00\x00\x0C\x00\x00\x00\x18\x00\x00\x00\x04\x00\x00\x04\x00\x00\x00\x24\x00\x00\x00\x02\x00\x00\x00\x10\x00\x00\x00\x00\x00\x00\x00\x00\x40\x00\x00\x40\x60\x00\x00\x00\x20\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x04\x00\x00\x80\x08\x00\x00\x80\x18\x00\x00\x00\x00\x00\x00\x80\x00\x00\x00\x80\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x80\x00\x00\x00\x00\x04\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00"
#define __BIN_2_TYPE 0x00000002
#define __BIN_2_SIZE 0x000001A6
#define __BIN_2 "\x16\x11\x00\x00\x00\x21\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x04\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x1A\x00\x00\x20\x00\x00\x28\x00\x00\x00\x00\x00\x00\x00\x00\x20\x00\x10\x20\x00\x62\x00\x00\x00\x80\x00\x00\x82\x00\x20\xA0\x00\x00\x84\x00\x00\x90\x00\x00\x88\x00\x00\x00\x00\x00\x10\x00\x00\x00\x00\x00\x80\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x40\x00\x00\x08\x00\x00\x00\x00\x00\x80\x00\x00\x00\x00\x00\x80\x00\x00\x60\x00\x00\x00\x00\x00\x00\x00\x00\x3C\x00\x00\x7E\x00\x00\x7E\x00\x00\x76\x00\x00\x7E\x00\x00\xF8\x00\x00\xF8\x00\xF8\xFE\x00\xFA\xF9\x00\x00\xE0\x00\x80\xF7\x00\xA0\xFD\x00\x00\xFC\x00\x00\xFC\x00\x00\xFC\x00\x00\xFC\x00\x00\xFC\x00\x00\xFC\x00\x00\xCC\x00\x00\xFC\x00\x00\xEC\x00\x00\xEC\x00\x00\xEC\x00\x00\xEC\x00\x00\xEC\x00\x00\xEC\x00\x00\xEC\x00\x00\xCC\x00\x00\xC6\x00\x00\xC6\x00\x00\xE0\x00\x00\x00\x00\xFF\xC3\x01\xFF\x81\x01\xFF\x00\x01\xFF\x00\x01\xFF\x00\x01\xFF\x00\x01\xFF\x01\x01\x00\x00\x00\x00\x00\x00\x00\x00\x00\x0F\x00\x00\x0F\x00\x00\x0F\x00\x00\xFF\x01\x00\xFF\x01\x00\xFF\x01\x00\xFF\x01\x00\xFF\x05\x00\xFF\x0D\x00\xFF\x3D\x00\xFF\xFD\x00\xFF\xED\x00\xFF\xED\x00\xFF\xE9\x00\xFF\xE1\x00\xFF\xA1\x00\xFF\x81\x00\xFF\x01\x00\xFF\x01\x00\xFF\x00\x00\x7F\x10\x00\x7F\x08\x00\xFF\x0F\x01\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x02\x00\x00\x08\x00\x00\x00\x00\x00\x80\x00\x00\x00\x00\x20\x00\x00\x00\x40\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x40\x00\x00\x08\x00\x00\x00\x00\x00\x34\x00\x00\x00\x00\x00\x00\x00\x00\x84\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x80\x00\x00\x04\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x80\x00\x00\x00\x00\x00"
#define __BIN_3_TYPE 0x00000002
#define __BIN_3_SIZE 0x000001A6
#define __BIN_3 "\x16\x13\x00\x00\x00\x21\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x04\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x1A\x00\x00\x20\x00\x00\x28\x00\x00\x00\x00\x00\x00\x00\x00\x20\x00\x10\x20\x00\x62\x00\x00\x00\x80\x00\x00\x82\x00\x20\xA0\x00\x00\x84\x00\x00\x90\x00\x00\x88\x00\x00\x00\x00\x00\x10\x00\x00\x00\x00\x00\x80\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x80\x00\x00\x04\x00\x00\x00\x00\x00\x00\x02\x00\x00\x00\x00\x00\x02\x00\x80\x01\x00\x00\x00\x00\x00\x00\x00\x3C\x00\x00\x7E\x00\x00\x7E\x00\x00\x76\x00\x00\x7E\x00\x00\xF8\x00\x00\xF8\x00\xF8\xFE\x00\xFA\xF9\x00\x00\xE0\x00\x80\xF7\x00\xA0\xFD\x00\x00\xFC\x00\x00\xFC\x00\x00\xFC\x00\x00\xFC\x00\x00\xFC\x00\x00\xFC\x00\x00\xCC\x00\x00\xFC\x00\x00\xEC\x00\x00\xCC\x01\x00\xCC\x01\x00\xC6\x01\x00\xC6\x01\x00\xC6\x01\x00\x86\x01\x00\x03\x03\x80\x01\x03\x80\x01\x03\x00\x80\x03\x00\x00\x00\xFF\xC3\x07\xFF\x81\x07\xFF\x00\x07\xFF\x00\x07\xFF\x00\x07\xFF\x00\x07\xFF\x01\x07\x00\x00\x06\x00\x00\x06\x00\x00\x06\x0F\x00\x06\x0F\x00\x06\x0F\x00\x06\xFF\x01\x06\xFF\x01\x06\xFF\x01\x06\xFF\x01\x06\xFF\x05\x06\xFF\x0D\x06\xFF\x3D\x06\xFF\xFD\x06\xFF\xED\x06\xFF\xCD\x05\xFF\xC9\x05\xFF\xC0\x05\xFF\x50\x05\xFF\x10\x05\xFF\x30\x04\x7F\x78\x00\x3F\x78\x00\x1F\x7C\x00\x3F\x3C\x00\xFF\x3F\x06\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x02\x00\x00\x08\x00\x00\x00\x00\x00\x80\x00\x00\x00\x00\x20\x00\x00\x00\x40\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x40\x00\x00\x08\x00\x00\x00\x00\x00\x34\x00\x00\x00\x00\x00\x00\x00\x00\x04\x01\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x01\x00\x02\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x02\x00\x00\x00\x00"

//These are the coordinates of the player.
I64 x=0,y=GR_HEIGHT/2;


//This is a FIFO (first-in first-out) data structure
//which keeps track of bullets.  When a new bullet is
//fired, the shots_in val is increased by one.
//When a bullet expires, the shots_out val is increased
//by one.  The in and out vals loop-around, back to
//the start of the arrays.
#define MAX_SHOTS 32
I64 shots_in=0,shots_out=0,sx[MAX_SHOTS],sy[MAX_SHOTS];



//This is a fifo which keeps track of the bad guys.
#define MAX_DEAD 32
I64 dead_in=0,dead_out=0,gx[MAX_DEAD],gy[MAX_DEAD];
BoolI8 g_dead[MAX_DEAD];


//This is a graphic for the player's character.
//Graphics are created and edited by pressing
//<CTRL-R>.  When created, they are assigned
//a num.  Press <CTRL-T> right now, to see
//the hidden definition for the picture.  The
//text in quotes, you can set to whatever you want,
//but the ending num can't be changed.	It
//is set when the graphic is created and is used
//in future references to the picture.



/* <1> <1> (image) */






 
/* <2> <2> (image) */
 
 
 
 
 

/* <3> <3> (image) */








//This routine updates the window.  It is called
//by the window manager system task.  The task refers to
//the task which owns the window.
U0 UpdateWin(TaskStruct *task)
{
  I64 i,j;

  //This creates an alias to the screen's
  //bitmap.  gr_dc is the bitmap which
  //is redrawn every time the screen is updated.
  GrDC *dc=GrDCAlias(gr_dc,task);

  //<CTRL-T> now to see the hidden part
  //where the picture num is encoded. IB stands
  //for "insert ptr to binary object".
  dc->bkcolor=BLACK;
  GrSpritePlot3(dc,x,y,0,__BIN_1);

  i=shots_out;
  while (i!=shots_in) {
    j=i++ &(MAX_SHOTS-1);
    GrLine(dc,sx[j],sy[j],sx[j]-2,sy[j]);
  }
  i=dead_out;
  while (i!=dead_in) {
    j=i++ &(MAX_SHOTS-1);
    dc->bkcolor=BLACK;
    if (!g_dead[j]) {
      if (gx[j]%10>5)
	GrSpritePlot3(dc,gx[j],gy[j],0,__BIN_2);
      else
	GrSpritePlot3(dc,gx[j],gy[j],0,__BIN_3);
    }
  }
  GrDCDel(dc);
  i=0;
  PlotWinStr(task,"If you aspire to making games,",BLUE<<4+YELLOW,0,i++);
  PlotWinStr(task,"you must learn to make-up rules.",BLUE<<4+YELLOW,0,i++);
}

U0 SongTask(U64 dummy=0)
{ //Randomly generate (by God :-)
  nounusedwarn dummy;
  Fs->task_end_cb=&SndTaskEndCB;
  MusicSettingsRst;
  while (TRUE) {
    Play("qDqDsDCDCetBCBqCqCqCqCqDqDsDCDCetBCBqCqCqCqCqCq"
	 "CsCCCCetCCBeBBeBBqBqBqCqCsCCCCetCCBeBBeBBqBqB");
  }
}

U0 TheDead()
{
  U64 i,j,i1,j1,k=0,sc=0,ch=0,msg_code,p1,p2;
  BoolI8 gun_on=FALSE;

  MenuPush(
  "File {"
  "  Abort(,CH_SHIFT_ESC);"
  "  Exit(,CH_ESC);"
  "}"
  "Play {"
  "  Fire(,CH_SPACE);"
  "  Up(,,SC_CURSOR_UP);"
  "  Down(,,SC_CURSOR_DOWN);"
      "}"
      );
  SettingsPush; //See [C:/LT/Adam/TaskSettings.APZ,3] SettingsPush
  WinMax;
  Fs->song_task=Spawn(&SongTask,NULL,"Song",Fs);
  WordStat(OFF);
  Fs->update_win=&UpdateWin;
  WinBorder(OFF);

  try {
    do {
      if (msg_code=ScanMsg(&p1,&p2,1<<MSG_KEY_DOWN | 1<<MSG_KEY_UP)) {
      //With KEYBOARD msgs, the two msg args
      //are the scan code and ascii val for the key.
	sc=p2; ch=p1;

	if (msg_code==MSG_KEY_DOWN) {
	//The least significant byte of the scan code
	//identifies the key.
	  if (sc.u8[0]==SC_CURSOR_DOWN) {
	    y+=10;
	    if (y>GR_HEIGHT-30)
	      y=GR_HEIGHT-30;
	  } else if (sc.u8[0]==SC_CURSOR_UP) {
	    y-=10;
	    if (y<0)
	      y=0;
	  } else if (sc.u8[0]==SC_CURSOR_RIGHT ||
	      ch==CH_SPACE)
	    gun_on=TRUE;
	} else if (msg_code==MSG_KEY_UP) {
	  if (sc.u8[0]==SC_CURSOR_RIGHT ||
	      ch==CH_SPACE)
	    gun_on=FALSE;
	}
      }

      i=shots_out;
      while (i!=shots_in) {
	j=i++ &(MAX_SHOTS-1);
	sx[j]+=5;
	if (sx[j]>GR_WIDTH)
	  shots_out++;
	else {
	  i1=dead_out;
	  while (i1!=dead_in) {
	    j1=i1++ &(MAX_DEAD-1);
	    if (gy[j1]<=sy[j]<=gy[j1]+38 &&
		gx[j1]<=sx[j]<=gx[j1]+40)
	      g_dead[j1]=TRUE;
	  }
	}
      }

      if (gun_on) {
	j=shots_in&(MAX_SHOTS-1);
	sx[j]=x+32;
	sy[j]=y+14;
	shots_in++;
      }

      //This runs one out of four passes through this loop.
      if (!(k % 4)) {
	i=dead_out;
	while (i!=dead_in) {
	  j=i++ &(MAX_DEAD-1);
	  gx[j]-=1;
	  if (gx[j]<25)
	    dead_out++;
	}
      }

      //This runs one out of 150 passes through this loop.
      if (!(k%150)) {
	j=dead_in&(MAX_DEAD-1);
	gx[j]=GR_WIDTH-30;
	gy[j]=RandU32%(GR_HEIGHT-50)+25;
	g_dead[j]=FALSE;
	dead_in++;
      }
      k++;

      Sleep(10); //Swap this task-out for 10 miliseconds.
    } while (ch!=CH_ESC && ch!=CH_SHIFT_ESC);
  } catch
    CatchAll;
  SettingsPop;
  MenuPop;
}

TheDead;	// run program when #included
