//**WARNING:** Don't return out of a catch{}.  This
//might get fixed.

//You can use [C:/LT/OSMain2/OSMain2.CPZ,15] CatchAll() in a catch statement
//and it will report args.

#define EXCEPT_1	1
#define EXCEPT_2	2
#define EXCEPT_3	3
#define EXCEPT_4	4

U0 Show()
{
  I64 i;
  PrintF("Fs->except_argc=%X\r\n",Fs->except_argc);
  for (i=0;i<Fs->except_argc;i++)
    PrintF("Fs->except_argv[%d]=%08X\r\n",i,Fs->except_argv[i]);
}



BoolI64 Pmt(I64 i)
{
  PrintF("%d ",i);
  return YorN;
}

U0 D1()
{
  PutS("D1\r\n");
  if (Pmt(1)) throw(EXCEPT_1);
  if (Pmt(2)) throw(EXCEPT_2,33,55,66);
  if (Pmt(5)) throw;
}

U0 D2()
{
  PutS("D2\r\n");
  try {
    D1;
    if (Pmt(4)) throw(EXCEPT_4);
  } catch {
    PutS("D2 handler\r\n");
    Show;
    if (Fs->except_argc>0) {
      if (Fs->except_argv[0]==EXCEPT_1) {
	PutS("Caught in D2\r\n");
	Fs->catch_except=TRUE;
      }
    }
  }
}

U0 Demo()
{
  try {
    D2;
    if (Pmt(3)) throw;
  } catch {
    PutS("Demo handler\r\n");
    Show;
    PutS("Ignore Except");
    if (YorN)
      Fs->ignore_except=TRUE;
    else
      Fs->catch_except=TRUE;
  }
}


Demo;
