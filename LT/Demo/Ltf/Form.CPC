class DStruct
{
//Use <CTRL-L> and select "Data" to generate the fmtstrs

//The number is the field from after the ':' to the end.
//  See [::/LT/Adam/Ltf/LtfPlain.APZ,DataTagWidth] Data Tag Width.

  F64 num1	fmtstr "$DA-T,32,\"F64   1:%12.8f\"$\r\n";
  F64 num2	fmtstr "$DA-T,32,\"F64   2:%12e\"$\r\n";
  I64 num3	fmtstr "$DA-T,32,\"I64   1:0x%016X\"$\r\n";
  I64 num4	fmtstr "$DA-T,32,\"I64   2:%12d\"$\r\n";
  U8 name[32]	fmtstr "$DA-P,31,\"Name  :%s\"$\r\n";
  LTDate dt	fmtstr "$DA-T,32,\"Date  :%8D\"$\r\n";
};

U0 FormDemo()
{
  DStruct gtl;
  LTDate datetime;

  gtl.num1=12.3e-4;  //Set defaults
  gtl.num2=4.5e3;
  gtl.num3=0x1234;
  gtl.num4=-567;
  StrCpy(&gtl.name,"John");
  datetime=GetCurTimeLTDate;
  gtl.dt=datetime;
  if (LtfFormDo(&gtl,"DStruct")) {
    "#1=%e\r\n",gtl.num1;
    "#2=%e\r\n",gtl.num2;
    "#3=%d\r\n",gtl.num3;
    "#4=%d\r\n",gtl.num4;
    "Name=%s\r\n",gtl.name;
    "Date=%D\r\n",gtl.dt;
  } else
    "Cancel\r\n";

  "\r\n\r\nClassRep Example\r\n";
  ClassRep("DStruct",&gtl);

//See also [C:/LT/Adam/Dbg2a.APZ,163] ClassRepD().
}

FormDemo;
