/*
A terminal task has a [C:/LT/OSMain/Adam1a.HPZ,1318] Ltf document structure that remains active even when you 
change Fs->update_win.  To prevent links in the [C:/LT/OSMain/Adam1a.HPZ,1318] Ltf from being activated when 
the user clicks in the window, do one of three things:
  
  A) [C:/LT/Adam/Ltf/LtfRecalc.APZ,1700] LtfBottom() followed by [C:/LT/Adam/Ltf/LtfRecalc.APZ,1734] LtfClear() to clear the [C:/LT/OSMain/Adam1a.HPZ,1318] Ltf so it has no active 
  widgets.
  
  B) Disable window manager button click checking with Fs->win_inhibit set to 
  mask [C:/LT/OSMain/Adam1a.HPZ,2958] WIF_IP_L and [C:/LT/OSMain/Adam1a.HPZ,2959] WIF_IP_R.  Only winmgr operations are inhibited -- 
  messages are still generated.
  
  C) Enter this code:
  Ltf *l=LtfCur;
  LtfRemQue(l);
  ...
  LtfInsQue(l,Fs->last_ltf);
  
  D) Hope the user just uses keyboard, not clicking the mouse in the window.
  
*/

U0 PlaceLinks()
{
  I64 i;
  for (i=0;i<100;i++)
    PutS("$LK,\"Dir\",\"MN:Dir\"$ ");
}

U0 VisibleWithNoFix()
{
  PlaceLinks;
  PopUpOk("Click widget to see problem.");
  PressAKey;
  PutS("Made keystroke and we lost our place, right?\r\n");
  LtfBottom;
  PressAKey;
}

U0 InvisibleWithNoFix()
{
  PlaceLinks;
  PopUpOk("Click widget to see problem.");
  SettingsPush; //See [C:/LT/Adam/TaskSettings.APZ,3] SettingsPush
  Fs->update_win=NULL;
  PressAKey;
  SettingsPop;
  PutS("Made keystroke and we lost our place, right?\r\n");
  LtfBottom;
  PressAKey;
}

U0 InvisibleWithClear()
{
  PlaceLinks;
  PopUpOk("Click,no problem.  Press A Key.");
  SettingsPush; //See [C:/LT/Adam/TaskSettings.APZ,3] SettingsPush
  Fs->update_win=NULL;
  LtfBottom;
  LtfClear;
  PressAKey;
  SettingsPop;
  PutS("Didn't lose our place, but screen was cleared right?\r\n");
  LtfBottom;
  PressAKey;
}

U0 InvisibleWithInhibit()
{
  PlaceLinks;
  PopUpOk("Click,no problem.  Press A Key.");
  SettingsPush; //See [C:/LT/Adam/TaskSettings.APZ,3] SettingsPush
  Fs->update_win=NULL;

//Only winmgr operations are inhibited.
//Messages are still generated.
  Fs->win_inhibit=WIF_IP_L|WIF_IP_R;

  PressAKey;
  SettingsPop;
  PutS("Didn't lose our place, right?\r\n");
  LtfBottom;
  PressAKey;
}

U0 InvisibleWithRemQue()
{
  Ltf *l=LtfCur;
  PlaceLinks;
  PopUpOk("Click,no problem.  Press A Key.");
  SettingsPush; //See [C:/LT/Adam/TaskSettings.APZ,3] SettingsPush
  Fs->update_win=NULL;
  LtfRemQue(l);
  PressAKey;
  LtfInsQue(l,Fs->last_ltf);
  SettingsPop;
  PutS("Didn't lose our place, right?\r\n");
}


U0 Quirk1Demo()
{
  VisibleWithNoFix;
  InvisibleWithNoFix;
  InvisibleWithClear;
  InvisibleWithInhibit;
  InvisibleWithRemQue;
}

Quirk1Demo;
