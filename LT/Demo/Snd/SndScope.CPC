U0 UpdateWin(TaskStruct *task)
{
  GrBitMap *base=GrAlias(gr_refreshed_base,task);
  SND_OUT_CONTAINER *buf=_hda->ostr0_buf[0];
  I64 i,x1,y1,x2,y2,
    cy=task->win_pixel_height>>1;

  base->color=BLUE;
  x2=0;y2=cy;
  for (i=0;i<SND_BUF_LEN;i+=2) {
    x1=i*task->win_pixel_width/SND_BUF_LEN;
    y1=cy-buf[i]*cy/snd_vol/MAX_I32;
    GrLine(base,x2,y2,x1,y1);
    x2=x1;y2=y1;
  }

  base->color=GREEN;
  x2=0;y2=cy;
  for (i=1;i<SND_BUF_LEN;i+=2) {
    x1=i*task->win_pixel_width/SND_BUF_LEN;
    y1=cy-buf[i]*cy/snd_vol/MAX_I32;
    GrLine(base,x2,y2,x1,y1);
    x2=x1; y2=y1;
  }

  GrDel(base);
}

U0 SndScope()
{
  if (snd_dev!=SD_HD_AUDIO) {
    PutS("Only works for HD Audio.\r\n");
    return;
  }
  SettingsPush; //See [C:/LT/Adam/TaskSettings.APZ,3] SettingsPush
  SetWinHorz(1,18);
  SetWinVert(15,TEXT_ROWS-2);
  Fs->update_win=&UpdateWin;
  GetChar;
  SettingsPop;
}

//This is so I can use this file stand alone
//or as a component of a program.
#if IncludeDepth<2
SndScope;
#endif
