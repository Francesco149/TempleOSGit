// imports all syms from the Adam Task's hash table.
#include "::/TempleOS/Kernel/AOTAdam.HPP"

//If you just want some of the symbols:
//#include "::/TempleOS/Kernel/AOT.HPP"

import U0 Routine1(U8 *st);
import U0 Routine2(U8 *st);
_import _MY_BEEP U0 MyBeep();

U0 Routine3(U8 *st)
{
  Print("Routine3:%s\n",st);
}


//Any routine with var parameters
//automatically has vars argc and argv[]
//whether it is main or not.
I64 ObjDemo(...)
{
  I64 i;
  MyBeep();
  if (argc>0) {
    for (i=0;i<argc;i++)
      Print("Arg %d:%08X\n",i,argv[i]);
    Routine1(argv[0]);
    if (argc>1) {
      Routine2(argv[1]);
      if (argc>2)
	Routine3(argv[2]);
    }
  } else
    Print("Pass string parameters for args 1 and 2.\n");
  return argc;
}

