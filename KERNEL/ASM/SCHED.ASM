////**************************PROCEDURE*************************
// IN:	 EDI = @TSS TO ADD TO QUEUE
//
	ALIGN	4,0x90
ADD_TASK_TO_QUEUE::
	PUSH	EBX
	PUSH	ESI
	PUSHFD
	CLI
	MOV	ESI,DWORD FS:[TSS_LAST_TSS]	//INSQUE
	MOV	EBX,DWORD FS:[TSS_ABSOLUTE_ADDRESS]
	MOV	DWORD TSS_NEXT_TSS[ESI],EDI
	MOV	DWORD TSS_LAST_TSS[EBX],EDI
	MOV	DWORD TSS_LAST_TSS[EDI],ESI
	MOV	DWORD TSS_NEXT_TSS[EDI],EBX
	BTS	DWORD [SYS_FLAGS],SYSF_TASK_LINKS_ALTERED
	POPFD
	POP	ESI
	POP	EBX
	RET
////**************************PROCEDURE*************************
	ALIGN	4,0x90
SAVE_CONTEXT::
	PUSHFD
	POP	DWORD FS:[TSS_EFLAGS]
	MOV	DWORD FS:[TSS_EAX],EAX
	MOV	DWORD FS:[TSS_EBX],EBX
	MOV	DWORD FS:[TSS_ECX],ECX
	MOV	DWORD FS:[TSS_EDX],EDX
	MOV	DWORD FS:[TSS_ESI],ESI
	MOV	DWORD FS:[TSS_EDI],EDI
	MOV	DWORD FS:[TSS_EBP],EBP

	BT	DWORD [SYS_FLAGS],SYSF_PREEMPTIVE
	JC	@@1
	BTR	WORD FS:[TSS_TASK_FLAGS],TSSF_PREEMPT
	JMP	@@2
@@1:	BTS	WORD FS:[TSS_TASK_FLAGS],TSSF_PREEMPT

@@2:	PUSH	EAX

	MOV	AX,SS
	MOV	WORD FS:[TSS_SS0],AX
	MOV	WORD FS:[TSS_SS],AX
	MOV	AX,CS
	MOV	WORD FS:[TSS_CS],AX
	MOV	AX,DS
	MOV	WORD FS:[TSS_DS],AX
	MOV	AX,ES
	MOV	WORD FS:[TSS_ES],AX
	MOV	AX,FS
	MOV	WORD FS:[TSS_FS],AX
	MOV	AX,GS
	MOV	WORD FS:[TSS_GS],AX
//	  SLDT	  WORD FS:TSS_LDTR
	POP	EAX
	RET
////**************************PROCEDURE*************************
	ALIGN	4,0x90
RESTORE_CONTEXT::
	MOV	EBX,DWORD FS:[TSS_EBX]
	MOV	ECX,DWORD FS:[TSS_ECX]
	MOV	EDX,DWORD FS:[TSS_EDX]
	MOV	ESI,DWORD FS:[TSS_ESI]
	MOV	EDI,DWORD FS:[TSS_EDI]
	MOV	EBP,DWORD FS:[TSS_EBP]

	MOV    AX,WORD FS:[TSS_SS]
	MOV    SS,AX
	MOV    AX,WORD FS:[TSS_DS]
	MOV    DS,AX
	MOV    AX,WORD FS:[TSS_ES]
	MOV    ES,AX
//	  MOV	 AX,WORD FS:[TSS_FS]
//	  MOV	 FS,AX
	MOV	AX,WORD FS:[TSS_GS]
	MOV	GS,AX

//	  LLDT	  WORD FS:[TSS_LDTR]
//	  LTR	  WORD FS:[TSS_SELECTOR]
	MOV	ESP,DWORD FS:[TSS_ESP]
	MOV	EAX,DWORD FS:[TSS_EAX]
	BT	WORD FS:[TSS_TASK_FLAGS],TSSF_PREEMPT
	JC	@@1
	BTR	DWORD [SYS_FLAGS],SYSF_PREEMPTIVE
	JMP	@@2
@@1:	BTS	DWORD [SYS_FLAGS],SYSF_PREEMPTIVE
@@2:	PUSH	DWORD FS:[TSS_EFLAGS]
	PUSH	DWORD FS:[TSS_CS]
	PUSH	DWORD FS:[TSS_EIP]
	IRET
////**************************PROCEDURE*************************
	ALIGN	4,0x90
END_TASK::
	CLI
	MOV	EAX,DWORD [SYS_EXTERN_TABLE]
	CALL	DWORD EXT_BLANK_CRT_TEXT*4[EAX]

	MOV	ESI,FS:[TSS_MEMORY_BLOCK_LIST]
	CALL	FREE_BLOCK_LIST

	MOV	ESI,DWORD FS:[TSS_NEXT_TSS]
	MOV	EDI,DWORD FS:[TSS_LAST_TSS]
	MOV	DWORD TSS_NEXT_TSS[EDI],ESI
	MOV	DWORD TSS_LAST_TSS[ESI],EDI
	BTS	DWORD [SYS_FLAGS],SYSF_TASK_LINKS_ALTERED

	PUSH	ESI
	MOV	ESI,DWORD FS:[TSS_ABSOLUTE_ADDRESS]
	CALL	RFREE
	POP	ESI

	XOR	EAX,EAX
	MOV	AX,FS
	MOV	EBX,DWORD [FREE_GDT_LIST]
	MOV	DWORD GDTTAB[EAX],EBX
	MOV	DWORD [FREE_GDT_LIST],EAX

	MOV	AX,WORD TSS_FS[ESI]
	MOV	FS,AX
	JMP	SWAP_IN_NEXT_PART2
////**************************PROCEDURE*************************
//IN	 EDI=TASK
	ALIGN	4,0x90
REMQUE_TASK::
	PUSH	ESI
	PUSHFD
	CLI

	PUSH	EDI
	MOV	ESI,DWORD TSS_NEXT_TSS[EDI]
	MOV	EDI,DWORD TSS_LAST_TSS[EDI]
	MOV	DWORD TSS_NEXT_TSS[EDI],ESI
	MOV	DWORD TSS_LAST_TSS[ESI],EDI
	BTS	DWORD [SYS_FLAGS],SYSF_TASK_LINKS_ALTERED
	POP	EDI

	POPFD
	POP	ESI
	RET
////**************************PROCEDURE*************************
	ALIGN	4,0x90
SWAP_IN_NEXT::
	PUSHFD
	CLI
	CALL	SAVE_CONTEXT
	MOV	EAX,SIN_RET
	MOV	DWORD FS:[TSS_EIP],EAX
	POP	DWORD FS:[TSS_EFLAGS]
	MOV	DWORD FS:[TSS_ESP],ESP
SIN_NEXT:
	MOV	ESI,DWORD FS:[TSS_NEXT_TSS]
	MOV	AX,WORD TSS_FS[ESI]
	MOV	FS,AX
SWAP_IN_NEXT_PART2::
	BTR	DWORD [SYS_FLAGS],SYSF_CTRL_ALT_DEL
	JC	DWORD CMD_REBOOT

	BTR	DWORD [SYS_FLAGS],SYSF_CTRL_ALT_ESC
	JNC	@@2
	CALL	SPAWN_USER_AND_ACTIVATE
	JMP	SWAP_IN_NEXT_PART3

@@2:	BTR	DWORD [SYS_FLAGS],SYSF_CTRL_ALT_TAB
	JNC	@@3
	CALL	CMD_ACTIVATE_NEXT_USER
	JMP	SWAP_IN_NEXT_PART3

@@3:	BTR	DWORD [SYS_FLAGS],SYSF_CTRL_ALT_X
	JC	KILL_ACTIVE_USER

SWAP_IN_NEXT_PART3::
	BT	WORD FS:[TSS_TASK_FLAGS],TSSF_KILL_TASK
	JNC	@@1
	MOV	DWORD FS:[TSS_EIP],END_TASK
	JMP	DWORD RESTORE_CONTEXT
@@1:	BT	WORD FS:[TSS_TASK_FLAGS],TSSF_SUSPENDED
	JC	SIN_NEXT
	JMP	DWORD RESTORE_CONTEXT
SIN_RET: RET

KILL_ACTIVE_USER::
	CALL	DEACTIVATE_USER
	PUSH	ESI
	CALL	ACTIVATE_NEXT_USER
	POP	ESI
	BT	WORD TSS_TASK_FLAGS[ESI],TSSF_LOCAL_USER
	JNC	SWAP_IN_NEXT_PART3
	MOV	EAX,DWORD TSS_FS[ESI]
	MOV	FS,AX
	JMP	DWORD END_TASK	      //No Return
////**************************PROCEDURE*************************
CMD_DESC: DB	 "USER CMD PROMPT",0;
	ALIGN	4,0x90
SPAWN_USER_AND_ACTIVATE::
	PUSHFD
	CLI
	PUSHAD
	CALL	CMD_DEACTIVATE_ALL
@@18:	MOV	EAX,USER_COMMAND_LINE
	MOV	ESI,CMD_DESC
	MOV	ECX,DEFAULT_STACK
	CALL	SPAWN_TASK
	MOV	ESI,DWORD [SYS_EXTERN_TABLE]
	MOV	ESI,DWORD EXT_WINDOW_TO_TOP*4[ESI]
	OR	ESI,ESI
	JZ	@@1
	PUSH	EDI
	CALL	ESI
	POP	EDI
@@1:	MOV	ESI,DWORD ROOT_TSS
	MOV	ESI,DWORD TSS_HASH_TABLE[ESI]
	MOV	EDI,DWORD TSS_HASH_TABLE[EDI]
	MOV	DWORD HT_NEXT[EDI],ESI
	POPAD
	POPFD
	RET
////**************************PROCEDURE*************************
//IN:	 ESI=CURRENT USER TSS
	ALIGN	4,0x90
ACTIVATE_NEXT_USER::
	PUSHFD
	CLI
	PUSH	EDI
	MOV	EDI,ESI
	MOV	ESI,DWORD TSS_NEXT_TSS[ESI]
@@14:	BT	WORD TSS_TASK_FLAGS[ESI],TSSF_LOCAL_USER
	JNC	@@19
	BTS	WORD TSS_TASK_FLAGS[ESI],TSSF_FOCUS
	JC	@@19
	MOV	EDI,DWORD [SYS_EXTERN_TABLE]
	MOV	EDI,DWORD EXT_WINDOW_TO_TOP*4[EDI]
	OR	EDI,EDI
	JZ	@@19B
	PUSHAD
	PUSH	ESI
	CALL	EDI
	ADD	ESP,4
	POPAD
	JMP	@@19B
@@19:	MOV	ESI,DWORD TSS_NEXT_TSS[ESI]
	CMP	ESI,EDI
	JNE	@@14
@@19B:	POP	EDI
	POPFD
	RET
////**************************PROCEDURE*************************
	ALIGN	4,0x90
CMD_DEACTIVATE_ALL::
	PUSH	ESI
	PUSHFD
	CLI
	PUSH	EDI
	MOV	ESI,DWORD FS:[TSS_ABSOLUTE_ADDRESS]
	MOV	EDI,ESI
@@11:	BTR	WORD TSS_TASK_FLAGS[ESI],TSSF_FOCUS
	MOV	ESI,DWORD TSS_NEXT_TSS[ESI]
	CMP	ESI,EDI
	JNE	@@11
	POP	EDI
	POPFD
	POP	ESI
	RET

////**************************PROCEDURE*************************
//OUT:	 ESI=TSS OF USER
	ALIGN	4,0x90
DEACTIVATE_USER::
	PUSHFD
	CLI
	PUSH	EDI
	MOV	ESI,DWORD FS:[TSS_ABSOLUTE_ADDRESS]
	MOV	EDI,ESI
@@11:	BT	WORD TSS_TASK_FLAGS[ESI],TSSF_FOCUS
	JNC	@@12
	BT	WORD TSS_TASK_FLAGS[ESI],TSSF_LOCAL_USER
	JC	@@13
@@12:	MOV	ESI,DWORD TSS_NEXT_TSS[ESI]
	CMP	ESI,EDI
	JNE	@@11
	JMP	@@13A
@@13:	BTR	WORD TSS_TASK_FLAGS[ESI],TSSF_FOCUS
@@13A:	POP	EDI
	POPFD
	RET

////**************************PROCEDURE*************************
//IN:	 ESI=CURRENT USER TSS
	ALIGN	4,0x90
CMD_ACTIVATE_NEXT_USER::
	PUSH	ESI
	CALL	DEACTIVATE_USER
	CALL	ACTIVATE_NEXT_USER
	POP	ESI
	RET
////**************************PROCEDURE*************************
	ALIGN	4,0x90
CMD_DEACTIVATE_USER::
	PUSH	ESI
	CALL	DEACTIVATE_USER
	MOV	EAX,ESI
	POP	ESI
	RET
	END
