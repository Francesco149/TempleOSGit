#help_index "DolDoc"

I64 DocOptEntry(CDoc *,CDocEntry *doc_e,I64 fuf_flags)
{
  U8 *st,*st2,**_dst;
  I64 i,res=0;

  if (doc_e->de_flags&DOCEF_LINK && doc_e->de_flags&(DOCEF_AUX_STR|DOCEF_TAG)) {
    if (doc_e->de_flags & DOCEF_AUX_STR && doc_e->de_flags & DOCEF_TAG &&
	  !MemCmp(doc_e->aux_str,"FI:",3) &&
	  !StrCmp(doc_e->aux_str+3,doc_e->tag)) {
      Free(doc_e->aux_str);
      doc_e->aux_str=NULL;
      doc_e->de_flags&=~DOCEF_AUX_STR;
      res++;
    }
    if (doc_e->de_flags & DOCEF_AUX_STR)
      _dst=&doc_e->aux_str;
    else
      _dst=&doc_e->tag;
    if (StrMatch(".Z",*_dst)) {
      st=DocLinkFile(*_dst);
      if (FileFind(st)) {
	if (IsDotZ(st))
	  st[StrLen(st)-2]=0;
	i=StrLen(st);
	if ((st2=StrMatch(st,*_dst)) && st2[i]=='.' && st2[i+1]=='Z') {
	  StrCpy(st2+i,st2+i+2);
	  res++;
	}
      }
      Free(st);
    }
    if (fuf_flags&FUF_RISKY) {
      if (doc_e->de_flags & DOCEF_AUX_STR) {
	if (st=StrMatch(".Z",doc_e->aux_str)) {
	  StrCpy(st,st+2);
	  res++;
	}
      }
      if (doc_e->de_flags&DOCEF_TAG) {
	if (st=StrMatch(".Z",doc_e->tag)) {
	  StrCpy(st,st+2);
	  res++;
	}
      }
    }
  }
  return res;
}

I64 DocOptDoc(CDoc *doc,I64 fuf_flags)
{//Optimize Doc.
  Bool unlock=DocLock(doc);
  I64 res=0;
  CDocEntry *doc_e;
  DocRecalc(doc);
  doc_e=doc->root.next;
  while (doc_e!=doc) {
    if (res+=DocOptEntry(doc,doc_e,fuf_flags))
      res+=DocOptEntry(doc,doc_e,fuf_flags);
    doc_e=doc_e->next;
  }
  DocRecalc(doc);
  if (unlock)
    DocUnlock(doc);
  return res;
}

I64 DocOptFile(U8 *filename,I64 fuf_flags)
{//Optimize File.
  I64 res;
  CDoc *doc=DocRead(filename);
  if (res=DocOptDoc(doc,fuf_flags)) {
    "-%d:%s\n",res,doc->filename.name;
    DocWrite(doc);
  }
  DocDel(doc);
  return res;
}
I64 DocOptLst(CDirEntry *tempde,I64 fuf_flags)
{
  I64 res=0;
  CDirEntry *tempde1;
  while (tempde) {
    tempde1=tempde->next;
    if (tempde->attr & RS_ATTR_DIR) {
      if (tempde->sub) {
	"Scanning Directory: %s\n",tempde->full_name;
	res+=DocOptLst(tempde->sub,fuf_flags);
      }
    } else
      res+=DocOptFile(tempde->full_name,fuf_flags);
    DirEntryDel(tempde);
    tempde=tempde1;
  }
  return res;
}
public I64 DocOpt(U8 *files_find_mask="*",U8 *fu_flags=NULL)
{//Optimize [C:/Doc/DolDocOverview.TXT.Z,1] DolDoc Files, eliminating aux_str's and .Z's.
//+R flag for aggressively risky.
  I64 fuf_flags=0;
  ScanFlags(&fuf_flags,Define("ST_FILE_UTIL_FLAGS"),"+r+T");
  ScanFlags(&fuf_flags,Define("ST_FILE_UTIL_FLAGS"),fu_flags);
  return DocOptLst(FilesFind(files_find_mask,fuf_flags&FUG_FILES_FIND),
	fuf_flags&~FUG_FILES_FIND);
}
