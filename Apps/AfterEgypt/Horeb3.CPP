U0 AnimateTask(I64)
{ //Preemption is off after Spawn
  I64 i;
  F64 t;
  Obj *tempo;
  while (TRUE) {
    t=tT;
    for (i=0;i<O_NUM_OBJS;i++) {
      tempo=&objs[i];
      if (tempo->type==OT_SHEEP || tempo->type==OT_GOAT1 || tempo->type==OT_GOAT2) {
	tempo->x+=tempo->dx;
	tempo->y+=tempo->dy;
	tempo->z+=tempo->dz;
	tempo->dx=5*Cos(0.5*t+tempo->theta);
	tempo->dz=5*Sin(0.5*t+tempo->theta);
      }
    }
    Sleep(20);
  }
}

U0 Init()
{
  Obj *tempo;
  I64 i,j,k,total=0;

  for (i=0;i<OT_NUM_TYPES;i++)
    total+=type_weights[i];

  MemSet(objs,0,sizeof(objs));
  for (i=0;i<O_NUM_OBJS;i++) {
    tempo=&objs[i];
    tempo->x=RandU32&0x1FFF-0x1000;
    tempo->y=0;
    tempo->z=RandU32&0x1FFF-0x1000;
    tempo->theta=2*pi*Rand;
    tempo->sym=RandU16&1;

    k=0;
    j=RandU32%total;
    while (TRUE) {
      j-=type_weights[k];
      if (j<0)
	break;
      else
	k++;
    }

    tempo->type=k;
  }
  objs[O_BURNING_BUSH].type=OT_BUSH1;

  vx=0; vz=0; theta=0;
  horeb_done=FALSE;
}

#define ANIMATE_STEPS	10

U0 AnimatethetaTask(F64 a)
{
  I64 i;
  for (i=0;i<ANIMATE_STEPS;i++) {
    theta+=a/ANIMATE_STEPS;
    Sleep(10);
  }
}

U0 AnimateVTask(F64 v)
{
  I64 i;
  for (i=0;i<ANIMATE_STEPS;i++) {
    vx+=v/ANIMATE_STEPS*Sin(theta);
    vz+=v/ANIMATE_STEPS*Cos(theta);
    Sleep(10);
  }
}

U0 Horeb()
{
  I64 ch,sc;
  MenuPush(
  "File {"
  "  Abort(,CH_SHIFT_ESC);"
  "  Exit(,CH_ESC);"
  "}"
  "Play {"
  "  Restart(,'\n');"
  "  Fwd(,,SC_CURSOR_UP);"
  "  Backward(,,SC_CURSOR_DOWN);"
  "  Left(,,SC_CURSOR_LEFT);"
  "  Right(,,SC_CURSOR_RIGHT);"
      "}"
      );
  SettingsPush(Fs,TSF_SAME_SONG); //See [C:/Adam/TaskSettings.CPP.Z,3] SettingsPush
  try {
    Fs->text_attr=YELLOW<<4+BLUE;
    Fs->win_inhibit=WIG_TASK_DFT-WIF_FOCUS-WIF_GRAB_SCROLL-WIF_MENU;
    WordStat;
    WinMax;
    WinBorder;
    Preempt(OFF);

    DocClear;
    "$BG,LTCYAN$"; NewLine(6);
    "$BG,YELLOW$"; NewLine(30);
    DocBibleVerse(,"Exodus,3:1",21);
    Init;
    Fs->animate_task=Spawn(&AnimateTask,NULL,"Animate",,Fs);
    Fs->draw_it=&DrawHoreb;
    while (!horeb_done) {
      while (ScanKey(&ch,&sc))
	switch (ch) {
	  case 0:
	    switch (sc.u8[0]) {
	      case SC_CURSOR_LEFT:
		Spawn(&AnimatethetaTask,(-pi/100)(I64),"Animate theta",,Fs);
		break;
	      case SC_CURSOR_RIGHT:
		Spawn(&AnimatethetaTask,( pi/100)(I64),"Animate theta",,Fs);
		break;
	      case SC_CURSOR_UP:
		Spawn(&AnimateVTask,(-40.0 )(I64),"Animate V",,Fs);
		break;
	      case SC_CURSOR_DOWN:
		Spawn(&AnimateVTask,( 40.0 )(I64),"Animate V",,Fs);
		break;
	    }
	    break;
	  case '\n':
	    Init;
	    break;
	  case CH_SHIFT_ESC:
	  case CH_ESC:
	    goto hr_done;
	}
      WinMgrSync;
    }
hr_done: //Don't goto out of try
  } catch
    Fs->catch_except=TRUE;
  DocClear;
  SettingsPop(Fs,TSF_SAME_SONG);
  MenuPop;
}
