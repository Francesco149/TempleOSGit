	ALIGN	4,0x90
INIT_MEM::
	MOV	DWORD [MEM_FREE_LIST],0
	MOV	EBX,DWORD [SYS_HEAP_BASE]

	MOV	EAX,DWORD [SYS_MEMBLKS]
	DEC	EAX

	MOV	DWORD [SYS_HEAP_LIMIT],EAX
	INC	EAX
	SUB	EAX,EBX
	SHR	EAX,PAGE_BITS
	MOV	EDI,DWORD [MEM_FREE_LIST]
	MOV	DWORD MB_NEXT[EBX],EDI
	MOV	DWORD [MEM_FREE_LIST],EBX
	MOV	DWORD MB_SIZE[EBX],EAX
	RET
////**************************PROCEDURE*************************
// IN:	EAX=NUM PAGES
//	EDX=Tss
//
// OUT:	ESI=BASE ADDRESS REL TO DS
//	EAX=Pages allocated
//
	ALIGN	4,0x90
ALLOCATE_MEM_BLKS::
	PUSH	EBX
	PUSH	ECX
	PUSH	EDI
	PUSHFD
	CLI

	CMP	EAX,FREE_PAGE_CACHE_SIZE
	JGE	@@10
	MOV	ESI,DWORD FREE_PAGE_CACHE[EAX*4]
	OR	ESI,ESI
	JZ	@@200
	MOV	ECX,DWORD MB_NEXT[ESI]
	MOV	DWORD FREE_PAGE_CACHE[EAX*4],ECX
	JMP	@@205

@@10:	BSR	ECX,EAX
	CMP	DWORD ROUND_UP_TABLE-4[ECX*4],EAX
	JE	@@15
	MOV	EAX,DWORD ROUND_UP_TABLE[ECX*4]
	MOV	ESI,DWORD FREE_PAGE_CACHE2[ECX*4]
	OR	ESI,ESI
	JZ	@@200
	MOV	EBX,DWORD MB_NEXT[ESI]
	MOV	DWORD FREE_PAGE_CACHE2[ECX*4],EBX
	JMP	@@205

@@15:	MOV	ESI,DWORD FREE_PAGE_CACHE2-4[ECX*4]
	OR	ESI,ESI
	JZ	@@200
	MOV	EBX,DWORD MB_NEXT[ESI]
	MOV	DWORD FREE_PAGE_CACHE2-4[ECX*4],EBX
	JMP	@@205

@@200:	MOV	ESI,MEM_FREE_LIST

@@201:	LEA	EBX,DWORD MB_NEXT[ESI]
	MOV	ESI,DWORD [EBX]
	OR	ESI,ESI
	JZ	@@300		//TODO: Out of mem
	CMP	DWORD MB_SIZE[ESI],EAX
	JB	@@201
	JNE	@@203

	MOV	EDI,DWORD MB_NEXT[ESI]
	MOV	DWORD MB_NEXT[EBX],EDI
	JMP	@@205

@@203:	SUB	DWORD MB_SIZE[ESI],EAX	  //UPDATE FREE ENTRY
	MOV	EDI,DWORD MB_SIZE[ESI]
	SHL	EDI,PAGE_BITS
	ADD	ESI,EDI 		//SET ESI TO @ALLOCATED PAGES

@@210:	MOV	DWORD MB_SIZE[ESI],EAX
@@205:	MOV	EDI,DWORD TSS_LAST_MEM_BLK[EDX]
	CALL	INSQUE

@@300:	POPFD
	POP	EDI
	POP	ECX
	POP	EBX
	RET
////**************************PROCEDURE*************************
// IN:	 ESI=BASE ADDRESS
	ALIGN	4,0x90
FREE_MEM_BLKS::
	OR	ESI,ESI
	JZ	@@100
	PUSH	EAX
	PUSH	EBX
	PUSH	ECX
	PUSHFD
	CLI

	CALL	REMQUE
	MOV	EAX,MB_SIZE[ESI]
	CMP	EAX,FREE_PAGE_CACHE_SIZE
	JGE	@@5
	MOV	EBX,DWORD FREE_PAGE_CACHE[EAX*4]
	MOV	DWORD MB_NEXT[ESI],EBX
	MOV	DWORD FREE_PAGE_CACHE[EAX*4],ESI
	JMP	@@101

@@5:	BSR	ECX,EAX
	MOV	EBX,DWORD FREE_PAGE_CACHE2-4[ECX*4]
	MOV	DWORD MB_NEXT[ESI],EBX
	MOV	DWORD FREE_PAGE_CACHE2-4[ECX*4],ESI

@@101:	POPFD
	POP	ECX
	POP	EBX
	POP	EAX
@@100:	RET
////**************************PROCEDURE*************************
// IN:	 ESI=BASE ADDRESS
	ALIGN	4,0x90
FREE_MEM_BLK_LIST::
	PUSH	EDX
	MOV	EDX,FS:[TSS_ABSOLUTE_ADDRESS]
	PUSH	EDI
	PUSHFD
	CLI
	MOV	EDI,EDX
	ADD	EDI,TSS_NEXT_MEM_BLK
@@1:	MOV	ESI,DWORD MB_NEXT[EDI]
	CMP	ESI,EDI
	JE	@@100
	CALL	FREE_MEM_BLKS
	JMP	@@1
@@100:	POPFD
	POP	EDI
	POP	EDX
	RET
////**************************PROCEDURE*************************
// IN:	 EAX=NUM BYTES
// OUT:  ESI=BASE ADDRESS
//
	ALIGN	4,0x90
ROUND_UP_TABLE:
	DD	2,4,8,16,32,64,128,256,512,1024,2048,4096,8192;
	DD	0x4000,0x8000,0x10000,0x20000,0x40000,0x80000;
	DD	0x100000,0x200000,0x400000,0x800000,0x1000000;
	DD	0x2000000,0x4000000,0x8000000,0x10000000;
	DD	0x20000000,0x40000000,0x80000000;
	ALIGN	4,0x90
CP_MALLOC::
	PUSH	EBP
	MOV	EBP,ESP
	PUSH	ESI
	PUSH	EDI
	PUSH	ECX
	MOV	EAX,SF_PARAM1[EBP]
	MOV	EDX,SF_PARAM2[EBP]
	OR	EDX,EDX
	JNZ	@@30
	MOV	EDX,DWORD FS:[TSS_ABSOLUTE_ADDRESS]
@@30:	PUSHFD
	CLI

	ADD	EAX,7
	AND	EAX,0xFFFFFFFC
	CMP	EAX,UUM_STRUCT_SIZE
	JGE	@@4
	MOV	EAX,UUM_STRUCT_SIZE
@@4:	CMP	EAX,HEAP_CACHE_SIZE
	JGE	@@10
	MOV	ESI,DWORD TSS_HEAP_CACHE[EAX+EDX]
	OR	ESI,ESI
	JZ	@@13
	MOV	ECX,DWORD UUM_NEXT[ESI]
	MOV	DWORD TSS_HEAP_CACHE[EAX+EDX],ECX
	JMP	DWORD @@5

@@10:	ADD	EAX,MB_STRUCT_SIZE+PAGE_SIZE-1
	SHR	EAX,PAGE_BITS
	CALL	ALLOCATE_MEM_BLKS
	SHL	EAX,PAGE_BITS
	SUB	EAX,MB_STRUCT_SIZE
	ADD	ESI,MB_STRUCT_SIZE
	JMP	@@5

@@13:	MOV	ESI,EDX
	ADD	ESI,TSS_MALLOC_FREE_LIST

@@1:	LEA	EBX,DWORD UUM_NEXT[ESI]
	MOV	ESI,DWORD [EBX]
	OR	ESI,ESI
	JNZ	@@2
	PUSH	EAX		//-****
	ADD	EAX,64*PAGE_SIZE-1
	SHR	EAX,PAGE_BITS
	CALL	ALLOCATE_MEM_BLKS
	LEA	ESI,DWORD MB_STRUCT_SIZE[ESI]
	SHL	EAX,PAGE_BITS
	SUB	EAX,MB_STRUCT_SIZE
	MOV	EDI,DWORD TSS_MALLOC_FREE_LIST[EDX]
	MOV	DWORD UUM_NEXT[ESI],EDI
	MOV	DWORD UUM_SIZE[ESI],EAX
	MOV	DWORD TSS_MALLOC_FREE_LIST[EDX],ESI
	MOV	EBX,EDX
	ADD	EBX,TSS_MALLOC_FREE_LIST
	POP	EAX		//+****
	JMP	@@3
@@2:	CMP	DWORD UUM_SIZE[ESI],EAX
	JL	@@1
	JNE	@@3

@@8:	MOV	EDI,DWORD UUM_NEXT[ESI]
	MOV	DWORD UUM_NEXT[EBX],EDI
	JMP	@@5

@@3:	SUB	DWORD UUM_SIZE[ESI],EAX    //UPDATE FREE ENTRY
	CMP	DWORD UUM_SIZE[ESI],UUM_STRUCT_SIZE
	JAE	@@7
	ADD	DWORD UUM_SIZE[ESI],EAX
	MOV	EAX,DWORD UUM_SIZE[ESI]
	JMP	@@8

@@7:	MOV	EDI,DWORD UUM_SIZE[ESI]
	ADD	ESI,EDI 		//SET ESI TO @ALLOCATED PAGES

@@5:	MOV	DWORD [ESI],EAX
	ADD	ESI,4

@@100:	POPFD
	MOV	EAX,ESI
	POP	ECX
	POP	EDI
	POP	ESI
	POP	EBP
	RET
////**************************PROCEDURE*************************
// IN:	 ESI=BASE ADDRESS
	ALIGN	4,0x90
CP_FREE::
	PUSH	EBP
	MOV	EBP,ESP
	PUSH	ESI
	PUSH	EDI
	PUSH	ECX

	MOV	ESI,SF_PARAM1[EBP]
	OR	ESI,ESI
	JZ	@@100
	MOV	EDX,SF_PARAM2[EBP]
	OR	EDX,EDX
	JNZ	@@30
	MOV	EDX,DWORD FS:[TSS_ABSOLUTE_ADDRESS]
@@30:	PUSHFD
	CLI

	SUB	ESI,4
	MOV	EAX,DWORD [ESI] //USED_USER_MEM_SIZE
	CMP	EAX,HEAP_CACHE_SIZE
	JGE	@@5
	MOV	UUM_SIZE[ESI],EAX
	MOV	EBX,DWORD TSS_HEAP_CACHE[EAX+EDX]
	MOV	DWORD UUM_NEXT[ESI],EBX
	MOV	DWORD TSS_HEAP_CACHE[EAX+EDX],ESI
	JMP	@@101

@@5:	SUB	ESI,MB_STRUCT_SIZE
	CALL	FREE_MEM_BLKS

@@101:	POPFD
@@100:	POP	ECX
	POP	EDI
	POP	ESI
	POP	EBP
	RET
////**************************PROCEDURE*************************
CP_MSIZE::
	PUSH	EBP
	MOV	EBP,ESP
	MOV	EAX,SF_PARAM1[EBP]
	OR	EAX,EAX
	JZ	@@100
	MOV	EAX,DWORD -4[EAX]
	SUB	EAX,4
@@100:	POP	EBP
	RET
