////**************************PROCEDURE*************************
	ALIGN	4,0x90
SAVE_CONTEXT::
	PUSHFD
	POP	DWORD FS:[TSS_EFLAGS]
	MOV	DWORD FS:[TSS_EAX],EAX
	MOV	DWORD FS:[TSS_EBX],EBX
	MOV	DWORD FS:[TSS_ECX],ECX
	MOV	DWORD FS:[TSS_EDX],EDX
	MOV	DWORD FS:[TSS_ESI],ESI
	MOV	DWORD FS:[TSS_EDI],EDI
	MOV	DWORD FS:[TSS_EBP],EBP

	BT	DWORD [SYS_FLAGS],SYSf_PREEMPTIVE
	JC	@@1
	BTR	DWORD FS:[TSS_TASK_FLAGS],TSSf_PREEMPT
	JMP	@@2
@@1:	BTS	DWORD FS:[TSS_TASK_FLAGS],TSSf_PREEMPT

@@2:	PUSH	EAX
	PUSH	ESI
	PUSH	EDI
	MOV	ESI,DWORD FS:[TSS_BPT_LIST]
@@3:	OR	ESI,ESI
	JZ	@@4
	MOV	EDI,DWORD BPT_ADDRESS[ESI]
	MOV	AL,BYTE BPT_BYTE[ESI]
	MOV	BYTE [EDI],AL
	MOV	ESI,DWORD BPT_NEXT[ESI]
	JMP	@@3

@@4:	POP	EDI
	POP	ESI
	POP	EAX
	RET
////**************************PROCEDURE*************************
	ALIGN	4,0x90
RESTORE_CONTEXT::
	INC	DWORD [SYS_SWAPS_CNTER]
	MOV	ESI,DWORD FS:[TSS_BPT_LIST]
@@3:	OR	ESI,ESI
	JZ	@@4
	MOV	EDI,DWORD BPT_ADDRESS[ESI]
	MOV	AL,BYTE [EDI]
	MOV	BYTE BPT_BYTE[ESI],AL
	MOV	AL,0xCC
	MOV	BYTE [EDI],AL
	MOV	ESI,DWORD BPT_NEXT[ESI]
	JMP	@@3

@@4:	MOV	EBX,DWORD FS:[TSS_EBX]
	MOV	ECX,DWORD FS:[TSS_ECX]
	MOV	EDX,DWORD FS:[TSS_EDX]
	MOV	ESI,DWORD FS:[TSS_ESI]
	MOV	EDI,DWORD FS:[TSS_EDI]
	MOV	EBP,DWORD FS:[TSS_EBP]

	MOV	ESP,DWORD FS:[TSS_ESP]
	MOV	EAX,DWORD FS:[TSS_EAX]
	BT	DWORD FS:[TSS_TASK_FLAGS],TSSf_PREEMPT
	JC	@@1
	BTR	DWORD [SYS_FLAGS],SYSf_PREEMPTIVE
	JMP	@@2
@@1:	BTS	DWORD [SYS_FLAGS],SYSf_PREEMPTIVE
@@2:	PUSH	DWORD FS:[TSS_EFLAGS]
	PUSH	DWORD FS:[TSS_CS]
	PUSH	DWORD FS:[TSS_EIP]
	IRET
////**************************PROCEDURE*************************
//IN	 EDI=TASK
	ALIGN	4,0x90
REMQUE_TASK:
	PUSH	ESI
	PUSHFD
	CLI

	PUSH	EDI
	MOV	ESI,DWORD TSS_NEXT_TSS[EDI]
	MOV	EDI,DWORD TSS_LAST_TSS[EDI]
	MOV	DWORD TSS_NEXT_TSS[EDI],ESI
	MOV	DWORD TSS_LAST_TSS[ESI],EDI
	BTS	DWORD [SYS_FLAGS],SYSf_TASK_LINKS_ALTERED
	POP	EDI

	POPFD
	POP	ESI
	RET
////**************************PROCEDURE*************************
	ALIGN	4,0x90
SWAP_IN_NEXT::
	PUSHFD
	CLI
	CALL	SAVE_CONTEXT
	MOV	EAX,SIN_RET
	MOV	DWORD FS:[TSS_EIP],EAX
	POP	DWORD FS:[TSS_EFLAGS]
	MOV	DWORD FS:[TSS_ESP],ESP
SIN_NEXT:
	MOV	ESI,DWORD FS:[TSS_NEXT_TSS]
	MOV	AX,WORD TSS_FS[ESI]
	MOV	FS,AX
SWAP_IN_NEXT_PART2::
	BTR	DWORD [SYS_FLAGS],SYSf_CTRL_ALT_DEL
	JC	DWORD CP_REBOOT

	BTR	DWORD [SYS_FLAGS],SYSf_CTRL_ALT_ESC
	JNC	@@2
	CALL	SPAWN_USER_AND_ACTIVATE
	JMP	SWAP_IN_NEXT_PART3

@@2:	BTR	DWORD [SYS_FLAGS],SYSf_CTRL_ALT_TAB
	JNC	@@3
	CALL	CP_ACTIVATE_NEXT_USER
	JMP	SWAP_IN_NEXT_PART3

@@3:	BTR	DWORD [SYS_FLAGS],SYSf_CTRL_ALT_X
	JC	END_ACTIVE_USER
	BTR	DWORD [SYS_FLAGS],SYSf_CTRL_ALT_C
	JC	BREAK_ACTIVE_USER

SWAP_IN_NEXT_PART3::
	BT	DWORD FS:[TSS_TASK_FLAGS],TSSf_KILL_TASK
	JNC	@@1
	CALL	END_TASK+4
	MOV	FS,AX
	JMP	SWAP_IN_NEXT_PART2
@@1:	BT	DWORD FS:[TSS_TASK_FLAGS],TSSf_SUSPENDED
	JC	SIN_NEXT
	JMP	DWORD RESTORE_CONTEXT
SIN_RET: RET

BREAK_ACTIVE_USER::
	MOV	ESI,DWORD [SYS_CUR_FOCUS_TASK]
	OR	ESI,ESI
	JZ	SWAP_IN_NEXT_PART3
	BT	DWORD TSS_TASK_FLAGS[ESI],TSSf_LOCAL_USER
	JNC	SWAP_IN_NEXT_PART3
	MOV	EAX,CP_BREAK+4
	MOV	DWORD TSS_EIP[ESI],EAX
	JMP	SWAP_IN_NEXT_PART3

END_ACTIVE_USER::
	CALL	DEACTIVATE_USER
	PUSH	ESI
	CALL	ACTIVATE_NEXT_USER
	POP	ESI
	OR	ESI,ESI
	JZ	SWAP_IN_NEXT_PART3
	BT	DWORD TSS_TASK_FLAGS[ESI],TSSf_LOCAL_USER
	JNC	SWAP_IN_NEXT_PART3
	BTS	DWORD TSS_TASK_FLAGS[ESI],TSSf_KILL_TASK
	JMP	SWAP_IN_NEXT_PART3
////**************************PROCEDURE*************************
MSG_USER_CMD_DESC:	DB	"USER CMD",0;
	ALIGN	4,0x90
SPAWN_USER_AND_ACTIVATE::
	PUSHFD
	CLI
	PUSHAD
	MOV	DWORD [SYS_CUR_FOCUS_TASK],0

	PUSH	DWORD DEFAULT_STACK
	PUSH	DWORD 0		///use root account
	PUSH	DWORD 0		//ROOT is parent
	PUSH	DWORD MSG_USER_CMD_DESC
	PUSH	DWORD CP_USER_CMD_LINE+4
	CALL	CP_SPAWN_TASK+4
	ADD	ESP,20

	PUSH	EAX
	MOV	ESI,MSG_WINDOW_TO_TOP
	CALL	FIND_EXTERN
	JZ	@@1
	CALL	ESI
@@1:	POP	EDI

	POPAD
	POPFD
	RET
////**************************PROCEDURE*************************
//IN:	 ESI=CUR USER TSS
	ALIGN	4,0x90
ACTIVATE_NEXT_USER:
	PUSHFD
	CLI
	PUSH	EDI
	OR	ESI,ESI
	JNZ	@@11
	MOV	ESI,ROOT_TSS
@@11:	MOV	EDI,ESI
	MOV	ESI,DWORD TSS_NEXT_TSS[ESI]
@@14:	BT	DWORD TSS_TASK_FLAGS[ESI],TSSf_LOCAL_USER
	JNC	@@19
	CMP	DWORD [SYS_CUR_FOCUS_TASK],ESI
	JE	@@19
	MOV	DWORD [SYS_CUR_FOCUS_TASK],ESI
	PUSH	ESI
	MOV	ESI,MSG_WINDOW_TO_TOP
	CALL	FIND_EXTERN
	MOV	EDI,ESI
	POP	ESI
	JZ	@@19B
	PUSHAD
	PUSH	ESI
	CALL	EDI
	ADD	ESP,4
	POPAD
	JMP	@@19B
@@19:	MOV	ESI,DWORD TSS_NEXT_TSS[ESI]
	CMP	ESI,EDI
	JNE	@@14
@@19B:	POP	EDI
	POPFD
	RET
////**************************PROCEDURE*************************
//OUT:	 ESI=TSS OF USER
	ALIGN	4,0x90
DEACTIVATE_USER:
	MOV	ESI,DWORD [SYS_CUR_FOCUS_TASK]
	MOV	DWORD [SYS_CUR_FOCUS_TASK],0
	RET

////**************************PROCEDURE*************************
//IN:	 ESI=CUR USER TSS
	ALIGN	4,0x90
CP_ACTIVATE_NEXT_USER::
	PUSH	ESI
	CALL	DEACTIVATE_USER
	CALL	ACTIVATE_NEXT_USER
	POP	ESI
	RET
////**************************PROCEDURE*************************
	ALIGN	4,0x90
CP_DEACTIVATE_USER::
	PUSH	ESI
	CALL	DEACTIVATE_USER
	MOV	EAX,ESI
	POP	ESI
	RET
